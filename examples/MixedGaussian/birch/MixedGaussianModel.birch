/**
 * Linear-nonlinear state-space model. The delayed sampling feature of Birch
 * results in a Rao--Blackwellized particle filter with locally-optimal
 * proposal being applied to this model.
 *
 * The model is detailed in [Lindsten and Schön (2010)](../#references).
 */
class MixedGaussianModel < StateSpaceModel<MixedGaussianParameter,
    MixedGaussianState,MixedGaussianObservation> {
  function initial(x:MixedGaussianState, θ:MixedGaussianParameter) {
    x.n ~ Gaussian(0.0, 1.0);
    x.l ~ Gaussian(vector(0.0, 3), identity(3));
  }

  function transition(x':MixedGaussianState, x:MixedGaussianState,
      θ:MixedGaussianParameter) {
    x'.n ~ Gaussian(atan(x.n) + dot(θ.b, x.l), θ.σ2_x_n);
    x'.l ~ Gaussian(θ.A*x.l, θ.Σ_x_l);
  }

  function observation(y:MixedGaussianObservation, x:MixedGaussianState,
      θ:MixedGaussianParameter) {
    y.n ~ Gaussian(0.1*copysign(pow(x.n, 2.0), x.n), θ.σ2_y_n);
    y.l ~ Gaussian(dot(θ.c, x.l), θ.σ2_y_l);
  }
}
