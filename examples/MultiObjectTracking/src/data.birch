/**
 * Create a simulated data set from simulation output.
 *
 * `--input`: path of an output file from a simulation run, containing the
 * ground truth.
 * `--output`: path to an output file to which to write the simulated data
 * set.
 */
program data(input:String <- "output/simulate.json",
    output:String <- "input/filter.json") {
  /* input */
  let reader <- Reader(input);
  let inputBuffer <- reader.scan();
  reader.close();
  
  /* read in the simulated observations from the track */
  θ:Global;
  y:Array<Array<Random<Real[_]>>>;

  let iter <- inputBuffer.walk();
  if iter.hasNext() {
    let array <- iter.next();
    let sample <- array.getObject("sample");
    if sample? {
      sample!.get("θ", θ);  
      sample!.get("y", y);
      let iter <- sample!.walk("z");
      while iter.hasNext() {
        let z <- iter.next();
        let t <- z.getInteger("t")!;
        let iter <- z.walk("y");
        while iter.hasNext() {
          let u <- iter.next();
          let v <- u!.getRealVector();
          if v? {
            w:Random<Real[_]>;
            w <- v![1..2];
            y.get(t).pushBack(w);
          }
          t <- t + 1;
        }
      }
    }
  }
  
  /* save the observations to the output file */
  outputBuffer:Buffer;
  outputBuffer.set("y", y);
  outputBuffer.set("θ", θ);

  let writer <- Writer(output);
  writer.print(outputBuffer);
  writer.close();
}
