/**
 * Multiple object model.
 */
final class Multi < Model {
  /**
   * Global parameters.
   */
  θ:Global;

  /**
   * Current tracks.
   */
  x:Array<Track>;

  /**
   * Current observations.
   */
  y:Array<Real[_]>;

  /**
   * All tracks up to current time.
   */
  xAll:Array<Track>;

  /**
   * All clutter observations up to current time.
   */
  yAll:Array<Array<Real[_]>>;

  override function simulate(t:Integer) {
    /* move current objects */
    x':Array<Track>;  // new current set of tracks
    let iter <- x.walk();
    while iter.hasNext() {
      let track <- iter.next();
      let ρ <- exp(logpdf_poisson(t - track.s - 1, θ.τ));
      let R <- 1.0 - cdf_poisson(t - track.s - 1, θ.τ) + ρ;
      let s <~ Bernoulli(1.0 - ρ/R);  // does the object survive?
      if s {
        track.simulate(t - track.s + 1);
        x'.pushBack(track);
      }
    }

    /* birth new objects */
    let N <~ Poisson(θ.λ);
    for n in 1..N {
      track:Track(t, θ);
      track.simulate();   // up to parameters
      track.simulate(1);  // up to initial time
      x'.pushBack(track);
      xAll.pushBack(track);
    }

    /* new current set of tracks */
    x <- x';

    if !y.empty() {
      /* associate observations */
      associate(t);
    } else {
      /* generate observations */
      generate(t);
    }
    yAll.pushBack(y);
  }

  function associate(t:Integer) {
    let iter <- x.walk();
    while iter.hasNext() {
      let track <- iter.next();
      let o <- track.y[t - track.s + 1];  // observation random variable
      if o.hasDistribution() {      
        /* propose an association */
        let N <- y.size();
        let p <- o.getDistribution();
        let q <- vector_lambda(\(n:Integer) -> { return p.pdf(y[n]); }, N);
        let Q <- sum(q)!;
        if Q > 0.0 {
          q <- q/Q;
          let n <~ Categorical(q);  // propose observation to associate with
          o <- y[n];  // observe
          y.erase(n);  // remove the observation for future associations
          factor -log(y.size());  // prior correction (uniform prior)
          factor -log(q[n]);  // proposal correction
        } else {
          factor -inf;  // detected, but all likelihoods (numerically) zero
        }
      }
    }
    
    /* remaining observations are clutter */
    let N <- y.size() - 1;
    N ~> Poisson(θ.μ);
    for n in 1..(N + 1) {
      y[n][1] ~> Uniform(θ.l, θ.u);
      y[n][2] ~> Uniform(θ.l, θ.u);
    }
  }

  function generate(t:Integer) {
    N:Integer <~ Poisson(θ.μ);
    for n in 1..(N + 1) {
      y[n] <- vector(0.0, 2);
      y[n][1] <~ Uniform(θ.l, θ.u);
      y[n][2] <~ Uniform(θ.l, θ.u);
    }
  }

  override function read(t:Integer, buffer:Buffer) {
    y <-? buffer.get<Array<Real[_]>>();
  }

  override function read(buffer:Buffer) {
    θ.l <-? buffer.get<Real>("l");
    θ.u <-? buffer.get<Real>("u");
    θ.d <-? buffer.get<Real>("d");
    θ.M <-? buffer.get<Real[_,_]>("M");
    θ.A <-? buffer.get<Real[_,_]>("A");
    θ.Q <-? buffer.get<Real[_,_]>("Q");
    θ.B <-? buffer.get<Real[_,_]>("B");
    θ.R <-? buffer.get<Real[_,_]>("R");
    θ.λ <-? buffer.get<Real>("λ");
    θ.μ <-? buffer.get<Real>("μ");
    θ.τ <-? buffer.get<Real>("τ");
  }

  override function write(buffer:Buffer) {
    buffer.set("l", θ.l);
    buffer.set("u", θ.u);
    buffer.set("d", θ.d);
    buffer.set("M", θ.M);
    buffer.set("A", θ.A);
    buffer.set("Q", θ.Q);
    buffer.set("B", θ.B);
    buffer.set("R", θ.R);
    buffer.set("λ", θ.λ);
    buffer.set("μ", θ.μ);
    buffer.set("τ", θ.τ);
    buffer.set("z", xAll);
    buffer.set("y", yAll);
  }
}
