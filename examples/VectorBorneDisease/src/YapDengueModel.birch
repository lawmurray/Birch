/**
 * Model for Yap case study.
 */
final class YapDengueModel < Model {
  /**
   * Human population.
   */
  h:SEIRComponent;

  /**
   * Mosquito population.
   */
  m:SEIRComponent;

  /**
   * Probability of a human case being observed.
   */
  ρ:Random<Real>;

  /**
   * Latent aggregate number of cases since last observation.
   */
  z:Integer <- 0;

  /**
   * Observed aggregate number of cases since last observation.
   */
  y:Integer?;

  function simulate() {
    h.ν <- 0.0;
    h.μ <- 1.0;
    h.λ ~ Beta(1.0, 1.0);
    h.δ ~ Beta(1.0 + 2.0/4.4, 3.0 - 2.0/4.4);
    h.γ ~ Beta(1.0 + 2.0/4.5, 3.0 - 2.0/4.5);

    m.ν <- 1.0/7.0;
    m.μ <- 6.0/7.0;
    m.λ ~ Beta(1.0, 1.0);
    m.δ ~ Beta(1.0 + 2.0/6.5, 3.0 - 2.0/6.5);
    m.γ <- 0.0;

    ρ ~ Beta(1.0, 1.0);
  }

  function simulate(t:Integer) {
    if t == 1 {
      /* initial state */
      h.n[t] <- 7370;
      h.i[t] <~ Poisson(5.0);
      h.i[t] <- h.i[t] + 1;
      h.e[t] <~ Poisson(5.0);
      h.r[t] <~ Uniform(0, h.n[t] - h.i[t] - h.e[t]);
      h.s[t] <- h.n[t] - h.e[t] - h.i[t] - h.r[t];

      h.Δs[t] <- 0;
      h.Δe[t] <- h.e[t];
      h.Δi[t] <- h.i[t];
      h.Δr[t] <- 0;
      
      u:Real;
      u <~ Uniform(-1.0, 2.0);
      m.n[t] <- Integer(h.n[t]*pow(10.0, u));
      m.r[t] <- 0;
      m.i[t] <- 0;
      m.e[t] <- 0;
      m.s[t] <- m.n[t];

      m.Δs[t] <- 0;
      m.Δe[t] <- 0;
      m.Δi[t] <- 0;
      m.Δr[t] <- 0;
    } else {
      /* transition of human population */
      τ_h:Integer;
      τ_h <~ Binomial(h.s[t - 1], 1.0 - exp(-m.i[t - 1]/Real(h.n[t - 1])));
      h.transfer(t, τ_h);

      /* transition of mosquito population */
      τ_m:Integer;
      τ_m <~ Binomial(m.s[t - 1], 1.0 - exp(-h.i[t - 1]/Real(h.n[t - 1])));
      m.transfer(t, τ_m);
    }

    /* observation */
    z <- h.Δi[t];
    if y? {
      y! ~> Binomial(z, ρ);
      z <- 0;
    }
  }

  function read(buffer:Buffer) {
    buffer.get("h", h);
    buffer.get("m", m);
    buffer.get("ρ", ρ);
  }

  function write(buffer:Buffer) {
    buffer.set("h", h);
    buffer.set("m", m);
    buffer.set("ρ", ρ);
  }

  function read(t:Integer, buffer:Buffer) {
    y <- buffer.get<Integer>();
  }
}
