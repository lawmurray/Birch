%code requires {
  #include "bi/lexer.hpp"
  #include "bi/build/Compiler.hpp"
}

%code {
  #include "bi/expression/all.hpp"
  #include "bi/statement/all.hpp"
  #include "bi/type/all.hpp"

  /**
   * Raw string stack.
   */
  std::stack<std::string> raws;

  /**
   * Push the current raw string onto the stack, and restart it.
   */
  void push_raw() {
    raws.push(raw.str());
    raw.str("");
  }

  /**
   * Pop a raw string from the stack.
   */
  std::string pop_raw() {
    std::string raw = raws.top();
    raws.pop();
    return raw;
  }

  /**
   * Make a location, without documentation string.
   */
  bi::Location* make_loc(YYLTYPE& loc) {
    return new bi::Location(compiler->file, loc.first_line, loc.last_line,
        loc.first_column, loc.last_column);
  }

  /**
   * Make a location, with documentation string.
   */
  bi::Location* make_doc_loc(YYLTYPE& loc) {
    return new bi::Location(compiler->file, loc.first_line, loc.last_line,
        loc.first_column, loc.last_column, pop_raw());
  }

  /**
   * Make an empty expression.
   */
  bi::Expression* empty_expr(YYLTYPE& loc) {
    return new bi::EmptyExpression(make_loc(loc));
  }

  /**
   * Make an empty statement.
   */
  bi::Statement* empty_stmt(YYLTYPE& loc) {
    return new bi::EmptyStatement(make_loc(loc));
  }

  /**
   * Make an empty type.
   */
  bi::Type* empty_type(YYLTYPE& loc) {
    return new bi::EmptyType(make_loc(loc));
  }
}

%union {
  bool valBool;
  int32_t valInt;
  double valReal;
  const char* valString;
  
  bi::Annotation valAnnotation;
  bi::Name* valName;
  bi::Expression* valExpression;
  bi::Type* valType;
  bi::Statement* valStatement;
}

%glr-parser
%expect-rr 3
%expect 6
// ^ Type?(...) vs x?
// ^ for generic function calls, f<Type>(expr) ambiguous with f < x > (expr),
//   the former is favoured by %dprec directives 
// ^ for generic class declaration, opening '<' ambiguous with inheritance,
//   but closing '>' disambiguates

%token <valString> PROGRAM CLASS TYPE FUNCTION FIBER OPERATOR AUTO
%token <valString> IF ELSE FOR IN WHILE DO ASSERT RETURN YIELD INSTANTIATED
%token <valString> CPP HPP THIS SUPER GLOBAL PARALLEL FINAL NIL
%token <valString> DOUBLE_BRACE_OPEN DOUBLE_BRACE_CLOSE NAME
%token <valString> BOOL_LITERAL INT_LITERAL REAL_LITERAL STRING_LITERAL
%token <valString> LEFT_OP RIGHT_OP LEFT_TILDE_OP RIGHT_TILDE_OP LEFT_QUERY_OP
%token <valString> AND_OP OR_OP LE_OP GE_OP EQ_OP NE_OP RANGE_OP

%type <valName> name binary_operator unary_operator observe_operator assume_operator

%type <valInt> size
%type <valAnnotation> for_annotation class_annotation function_annotation

%type <valName> assign_operator

%type <valExpression> prefix_operator
%type <valExpression> multiplicative_operator additive_operator relational_operator
%type <valExpression> equality_operator logical_and_operator logical_or_operator
%type <valExpression> bool_literal int_literal real_literal string_literal
%type <valExpression> literal identifier overloaded_identifier
%type <valExpression> this_expression super_expression nil_expression
%type <valExpression> parens_expression sequence_expression cast_expression
%type <valExpression> function_expression primary_expression
%type <valExpression> slice index_expression index_list
%type <valExpression> postfix_expression postfix_query_expression prefix_expression
%type <valExpression> multiplicative_expression additive_expression
%type <valExpression> relational_expression equality_expression
%type <valExpression> logical_and_expression logical_or_expression observe_expression
%type <valExpression> assign_expression expression_list expression optional_expression
%type <valExpression> local_variable parameter parameter_list parameters optional_parameters
%type <valExpression> span_expression span_list brackets option option_list options
%type <valExpression> arguments optional_arguments
%type <valExpression> value optional_value for_index
%type <valExpression> generic generic_list generics optional_generics

%type <valStatement> global_variable_declaration local_variable_declaration
%type <valStatement> function_declaration fiber_declaration program_declaration
%type <valStatement> member_variable_declaration member_function_declaration
%type <valStatement> member_fiber_declaration
%type <valStatement> binary_operator_declaration unary_operator_declaration
%type <valStatement> conversion_operator_declaration assignment_operator_declaration
%type <valStatement> class_declaration basic_declaration
%type <valStatement> cpp hpp expression_statement assume_statement if for
%type <valStatement> while do_while assertion return yield instantiated
%type <valStatement> statement statements optional_statements
%type <valStatement> class_statement class_statements optional_class_statements
%type <valStatement> file_statement file_statements optional_file_statements
%type <valStatement> braces optional_braces
%type <valStatement> class_braces optional_class_braces

%type <valType> basic_type class_type base_type unknown_type member_type
%type <valType> tuple_type sequence_type postfix_type function_type type
%type <valType> type_list parameter_type_list parameter_types
%type <valType> result optional_result
%type <valType> generic_argument generic_argument_list generic_arguments optional_generic_arguments

%type <valBool> weak_modifier

%locations

%start file
%%


/***************************************************************************
 * Names                                                                   *
 ***************************************************************************/

name
    : NAME  { $$ = new bi::Name($1); }
    ;


/***************************************************************************
 * Expressions                                                             *
 ***************************************************************************/

bool_literal
    : BOOL_LITERAL  { $$ = new bi::Literal<bool>($1, new bi::BasicType(new bi::Name("Boolean")), make_loc(@$)); }
    ;

int_literal
    : INT_LITERAL  { $$ = new bi::Literal<int64_t>($1, new bi::BasicType(new bi::Name("Integer")), make_loc(@$)); }
    ;

real_literal
    : REAL_LITERAL  { $$ = new bi::Literal<double>($1, new bi::BasicType(new bi::Name("Real")), make_loc(@$)); }
    ;

string_literal
    : STRING_LITERAL  { $$ = new bi::Literal<const char*>($1, new bi::BasicType(new bi::Name("String")), make_loc(@$)); }
    ;

literal
    : bool_literal
    | int_literal
    | real_literal
    | string_literal
    ;

identifier
    : name  { $$ = new bi::Identifier<bi::Unknown>($1, make_loc(@$)); }
    ;
    
overloaded_identifier
    : name generic_arguments  { $$ = new bi::OverloadedIdentifier<bi::Unknown>($1, $2, make_loc(@$)); }
    ;

parens_expression
    : '(' expression_list ')'  { $$ = new bi::Parentheses($2, make_loc(@$)); }
    ;

sequence_expression
    : '[' expression_list ']'  { $$ = new bi::Sequence($2, make_loc(@$)); }
    | '[' ']'                  { $$ = new bi::Sequence(empty_expr(@$), make_loc(@$)); }
    ;

cast_expression
    : name optional_generic_arguments '?' '(' expression ')'  { $$ = new bi::Cast(new bi::UnknownType(false, $1, $2, make_loc(@$)), $5, make_loc(@$)); }
    ;

function_expression
    : '@' parameters optional_result optional_braces  { $$ = new bi::LambdaFunction($2, $3, $4, make_loc(@$)); }
    ;

this_expression
    : THIS  { $$ = new bi::This(make_loc(@$)); }
    ;

super_expression
    : SUPER  { $$ = new bi::Super(make_loc(@$)); }
    ;

nil_expression
    : NIL  { $$ = new bi::Nil(make_loc(@$)); }
    ;

primary_expression
    : literal
    | identifier
    | overloaded_identifier
    | parens_expression
    | sequence_expression
    | cast_expression
    | function_expression
    | this_expression
    | nil_expression
    ;

index_expression
    : expression RANGE_OP expression  { $$ = new bi::Range($1, $3, make_loc(@$)); }
    | expression                      { $$ = new bi::Index($1, make_loc(@$)); }
    ;
    
index_list
    : index_expression
    | index_expression ',' index_list  { $$ = new bi::ExpressionList($1, $3, make_loc(@$)); }
    ;

slice
    : '[' index_list ']'  { $$ = $2; }
    ;

postfix_expression
    : primary_expression
    | super_expression '.' identifier    { $$ = new bi::Member($1, $3, make_loc(@$)); }
    | GLOBAL '.' identifier              { $$ = new bi::Global($3, make_loc(@$)); }
    | postfix_expression '.' identifier  { $$ = new bi::Member($1, $3, make_loc(@$)); }
    | postfix_expression slice           { $$ = new bi::Slice($1, $2, make_loc(@$)); }
    | postfix_expression arguments       { $$ = new bi::Call($1, $2, make_loc(@$)); }
    | postfix_expression '!'             { $$ = new bi::Get($1, make_loc(@$)); }
    ;

postfix_query_expression
    : postfix_expression
    | postfix_expression '?'  { $$ = new bi::Query($1, make_loc(@$)); }
    ;

prefix_operator
    : '+'  { $$ = new bi::OverloadedIdentifier<bi::UnaryOperator>(new bi::Name("+"), empty_type(@$), make_loc(@$)); }
    | '-'  { $$ = new bi::OverloadedIdentifier<bi::UnaryOperator>(new bi::Name("-"), empty_type(@$), make_loc(@$)); }
    | '!'  { $$ = new bi::OverloadedIdentifier<bi::UnaryOperator>(new bi::Name("!"), empty_type(@$), make_loc(@$)); }
    ;

prefix_expression
    : postfix_query_expression
    | prefix_operator prefix_expression  { $$ = new bi::UnaryCall($1, $2, make_loc(@$)); }
    ;

multiplicative_operator
    : '*'  { $$ = new bi::OverloadedIdentifier<bi::BinaryOperator>(new bi::Name("*"), empty_type(@$), make_loc(@$)); }
    | '/'  { $$ = new bi::OverloadedIdentifier<bi::BinaryOperator>(new bi::Name("/"), empty_type(@$), make_loc(@$)); }
    ;

multiplicative_expression
    : prefix_expression
    | multiplicative_expression multiplicative_operator prefix_expression  { $$ = new bi::BinaryCall($2, new bi::Binary($1, $3, make_loc(@$)), make_loc(@$)); }
    ;

additive_operator
    : '+'  { $$ = new bi::OverloadedIdentifier<bi::BinaryOperator>(new bi::Name("+"), empty_type(@$), make_loc(@$)); }
    | '-'  { $$ = new bi::OverloadedIdentifier<bi::BinaryOperator>(new bi::Name("-"), empty_type(@$), make_loc(@$)); }
    ;

additive_expression
    : multiplicative_expression
    | additive_expression additive_operator multiplicative_expression  { $$ = new bi::BinaryCall($2, new bi::Binary($1, $3, make_loc(@$)), make_loc(@$)); }
    ;

relational_operator
    : '<'    { $$ = new bi::OverloadedIdentifier<bi::BinaryOperator>(new bi::Name("<"), empty_type(@$), make_loc(@$)); }
    | '>'    { $$ = new bi::OverloadedIdentifier<bi::BinaryOperator>(new bi::Name(">"), empty_type(@$), make_loc(@$)); }
    | LE_OP  { $$ = new bi::OverloadedIdentifier<bi::BinaryOperator>(new bi::Name("<="), empty_type(@$), make_loc(@$)); }
    | GE_OP  { $$ = new bi::OverloadedIdentifier<bi::BinaryOperator>(new bi::Name(">="), empty_type(@$), make_loc(@$)); }
    ;

/* here we resolve the ambiguity between a function call with generic
 * arguments, f<Type>(expr), and an expression with relational operators
 * such as x < y > (z), favouring the former, as the latter is both
 * unusual and can be readily disambiguated with parentheses */
relational_expression
    : additive_expression                                            %dprec 1
    | relational_expression relational_operator additive_expression  %dprec 2  { $$ = new bi::BinaryCall($2, new bi::Binary($1, $3, make_loc(@$)), make_loc(@$)); }
    | overloaded_identifier arguments                                %dprec 3  { $$ = new bi::Call($1, $2, make_loc(@$)); }
    | GLOBAL '.' overloaded_identifier arguments                     %dprec 3  { $$ = new bi::Call(new bi::Global($3, make_loc(@$)), $4, make_loc(@$)); }
    ;

equality_operator
    : EQ_OP  { $$ = new bi::OverloadedIdentifier<bi::BinaryOperator>(new bi::Name("=="), empty_type(@$), make_loc(@$)); }
    | NE_OP  { $$ = new bi::OverloadedIdentifier<bi::BinaryOperator>(new bi::Name("!="), empty_type(@$), make_loc(@$)); }
    ;

equality_expression
    : relational_expression
    | equality_expression equality_operator relational_expression  { $$ = new bi::BinaryCall($2, new bi::Binary($1, $3, make_loc(@$)), make_loc(@$)); }
    ;

logical_and_operator
    : AND_OP  { $$ = new bi::OverloadedIdentifier<bi::BinaryOperator>(new bi::Name("&&"), empty_type(@$), make_loc(@$)); }
    ;

logical_and_expression
    : equality_expression
    | logical_and_expression logical_and_operator equality_expression  { $$ = new bi::BinaryCall($2, new bi::Binary($1, $3, make_loc(@$)), make_loc(@$)); }
    ;

logical_or_operator
    : OR_OP  { $$ = new bi::OverloadedIdentifier<bi::BinaryOperator>(new bi::Name("||"), empty_type(@$), make_loc(@$)); }
    ;

logical_or_expression
    : logical_and_expression
    | logical_or_expression logical_or_operator logical_and_expression  { $$ = new bi::BinaryCall($2, new bi::Binary($1, $3, make_loc(@$)), make_loc(@$)); }
    ;

observe_operator
    : RIGHT_TILDE_OP  { $$ = new bi::Name("~>"); }
    ;

observe_expression
    : logical_or_expression
    | observe_expression observe_operator logical_or_expression  { $$ = new bi::Assign($1, $2, $3, make_loc(@$)); }
    ;

assign_operator
    : LEFT_OP        { $$ = new bi::Name("<-"); }
    | LEFT_TILDE_OP  { $$ = new bi::Name("<~"); }
    ;

assign_expression
    : observe_expression
    | observe_expression assign_operator assign_expression  { $$ = new bi::Assign($1, $2, $3, make_loc(@$)); }
    ;

expression
    : assign_expression
    ;

optional_expression
    : expression
    |             { $$ = empty_expr(@$); }
    ;

expression_list
    : expression
    | expression ',' expression_list  { $$ = new bi::ExpressionList($1, $3, make_loc(@$)); }
    ;

local_variable
    : name ':' type                             { $$ = new bi::LocalVariable(bi::NONE, $1, $3, empty_expr(@$), empty_expr(@$), empty_expr(@$), make_loc(@$)); }
    | name ':' type arguments                   { $$ = new bi::LocalVariable(bi::NONE, $1, $3, empty_expr(@$), $4, empty_expr(@$), make_loc(@$)); }
    | name ':' type value                       { $$ = new bi::LocalVariable(bi::NONE, $1, $3, empty_expr(@$), empty_expr(@$), $4, make_loc(@$)); }
    | AUTO name value                           { $$ = new bi::LocalVariable(bi::AUTO, $2, empty_type(@$), empty_expr(@$), empty_expr(@$), $3, make_loc(@$)); }
    | name ':' postfix_type brackets            { $$ = new bi::LocalVariable(bi::NONE, $1, $3, $4, empty_expr(@$), empty_expr(@$), make_loc(@$)); }
    | name ':' postfix_type brackets arguments  { $$ = new bi::LocalVariable(bi::NONE, $1, $3, $4, $5, empty_expr(@$), make_loc(@$)); }
    | name ':' postfix_type brackets value      { $$ = new bi::LocalVariable(bi::NONE, $1, $3, $4, empty_expr(@$), $5, make_loc(@$)); }
    ;

span_expression
    : expression   { $$ = new bi::Span($1, make_loc(@$)); }
    ;
    
span_list
    : span_expression
    | span_expression ',' span_list  { $$ = new bi::ExpressionList($1, $3, make_loc(@$)); }
    ;

brackets
    : '[' span_list ']'  { $$ = $2; }
    ;

parameters
    : '(' ')'                 { $$ = empty_expr(@$); }
    | '(' parameter_list ')'  { $$ = $2; }
    ;

optional_parameters
    : parameters
    |             { $$ = empty_expr(@$); }
    ;

parameter_list
    : parameter
    | parameter ',' parameter_list  { $$ = new bi::ExpressionList($1, $3, make_loc(@$)); }
    ;

parameter
    : name ':' type  { $$ = new bi::Parameter(bi::NONE, $1, $3, empty_expr(@$), make_loc(@$)); }
    ;

options
    : '(' ')'              { $$ = empty_expr(@$); }
    | '(' option_list ')'  { $$ = $2; }
    ;

option_list
    : option
    | option ',' option_list  { $$ = new bi::ExpressionList($1, $3, make_loc(@$)); }
    ;

option
    : name ':' type optional_value  { $$ = new bi::Parameter(bi::NONE, $1, $3, $4, make_loc(@$)); }
    ;

arguments
    : '(' ')'                  { $$ = empty_expr(@$); }
    | '(' expression_list ')'  { $$ = $2; }
    ;

optional_arguments
    : arguments
    |            { $$ = empty_expr(@$); }
    ;

size
    : '_'           { $$ = 1; }
    | '_' ',' size  { $$ = $3 + 1; }
    ;

generics
    : '<' '>'               { $$ = empty_expr(@$); }
    | '<' generic_list '>'  { $$ = $2; }
    ;

generic_list
    : generic
    | generic ',' generic_list  { $$ = new bi::ExpressionList($1, $3, make_loc(@$)); }
    ;

generic
    : name           { $$ = new bi::Generic(bi::NONE, $1, empty_type(@$), make_loc(@$)); }
    ;

optional_generics
    : generics
    |           { $$ = empty_expr(@$); }
    ;

generic_arguments
    : '<' '>'                        { $$ = empty_type(@$); }
    | '<' generic_argument_list '>'  { $$ = $2; }
    ;

generic_argument_list
    : generic_argument
    | generic_argument ',' generic_argument_list  { $$ = new bi::TypeList($1, $3, make_loc(@$)); }
    ;

generic_argument
    : type
    ;
    
optional_generic_arguments
    : generic_arguments
    |                    { $$ = empty_type(@$); }
    ;


/***************************************************************************
 * Statements                                                              *
 ***************************************************************************/

global_variable_declaration
    : name ':' type ';'                             { push_raw(); $$ = new bi::GlobalVariable(bi::NONE, $1, $3, empty_expr(@$), empty_expr(@$), empty_expr(@$), make_doc_loc(@$)); }
    | name ':' type arguments ';'                   { push_raw(); $$ = new bi::GlobalVariable(bi::NONE, $1, $3, empty_expr(@$), $4, empty_expr(@$), make_doc_loc(@$)); }
    | name ':' type value ';'                       { push_raw(); $$ = new bi::GlobalVariable(bi::NONE, $1, $3, empty_expr(@$), empty_expr(@$), $4, make_doc_loc(@$)); }
    | AUTO name value ';'                           { push_raw(); $$ = new bi::GlobalVariable(bi::AUTO, $2, empty_type(@$), empty_expr(@$), empty_expr(@$), $3, make_loc(@$)); }
    | name ':' postfix_type brackets ';'            { push_raw(); $$ = new bi::GlobalVariable(bi::NONE, $1, $3, $4, empty_expr(@$), empty_expr(@$), make_doc_loc(@$)); }
    | name ':' postfix_type brackets arguments ';'  { push_raw(); $$ = new bi::GlobalVariable(bi::NONE, $1, $3, $4, $5, empty_expr(@$), make_doc_loc(@$)); }
    ;

local_variable_declaration
    : local_variable ';'  { push_raw(); $$ = new bi::ExpressionStatement($1, make_doc_loc(@$)); }
    ;

member_variable_declaration
    : name ':' type ';'                             { push_raw(); $$ = new bi::MemberVariable(bi::NONE, $1, $3, empty_expr(@$), empty_expr(@$), empty_expr(@$), make_doc_loc(@$)); }
    | name ':' type arguments ';'                   { push_raw(); $$ = new bi::MemberVariable(bi::NONE, $1, $3, empty_expr(@$), $4, empty_expr(@$), make_doc_loc(@$)); }
    | name ':' type value ';'                       { push_raw(); $$ = new bi::MemberVariable(bi::NONE, $1, $3, empty_expr(@$), empty_expr(@$), $4, make_doc_loc(@$)); }
    | AUTO name value ';'                           { push_raw(); $$ = new bi::MemberVariable(bi::AUTO, $2, empty_type(@$), empty_expr(@$), empty_expr(@$), $3, make_loc(@$)); }
    | name ':' postfix_type brackets ';'            { push_raw(); $$ = new bi::MemberVariable(bi::NONE, $1, $3, $4, empty_expr(@$), empty_expr(@$), make_doc_loc(@$)); }
    | name ':' postfix_type brackets arguments ';'  { push_raw(); $$ = new bi::MemberVariable(bi::NONE, $1, $3, $4, $5, empty_expr(@$), make_doc_loc(@$)); }
    ;

function_annotation
    :           { $$ = bi::NONE; }
    ;

function_declaration
    : function_annotation FUNCTION name optional_generics parameters optional_result { push_raw(); } optional_braces  { $$ = new bi::Function($1, $3, $4, $5, $6, $8, make_doc_loc(@$)); }
    ;

fiber_declaration
    : function_annotation FIBER name optional_generics parameters optional_result { push_raw(); } optional_braces  { $$ = new bi::Fiber($1, $3, $4, $5, new bi::FiberType($6, make_loc(@$)), $8, make_doc_loc(@$)); }
    ;

program_declaration
    : PROGRAM name options { push_raw(); } optional_braces  { $$ = new bi::Program($2, $3, $5, make_doc_loc(@$)); }
    ;

member_function_declaration
    : FUNCTION name parameters optional_result { push_raw(); } optional_braces  { $$ = new bi::MemberFunction(bi::NONE, $2, $3, $4, $6, make_doc_loc(@$)); }
    ;

member_fiber_declaration
    : FIBER name parameters optional_result { push_raw(); } optional_braces  { $$ = new bi::MemberFiber(bi::NONE, $2, $3, new bi::FiberType($4, make_loc(@$)), $6, make_doc_loc(@$)); }
    ;
    
binary_operator
    : '*'     { $$ = new bi::Name('*'); }
    | '/'     { $$ = new bi::Name('/'); }
    | '+'     { $$ = new bi::Name('+'); }
    | '-'     { $$ = new bi::Name('-'); }
    | '<'     { $$ = new bi::Name('<'); }  // handle specially
    | '>'     { $$ = new bi::Name('>'); }
    | LE_OP   { $$ = new bi::Name("<="); }
    | GE_OP   { $$ = new bi::Name(">="); }
    | EQ_OP   { $$ = new bi::Name("=="); }
    | NE_OP   { $$ = new bi::Name("!="); }
    | AND_OP  { $$ = new bi::Name("&&"); }
    | OR_OP   { $$ = new bi::Name("||"); }
    ;    

unary_operator
    : '+'     { $$ = new bi::Name('+'); }
    | '-'     { $$ = new bi::Name('-'); }
    | '!'     { $$ = new bi::Name('!'); }
    ;    

binary_operator_declaration
    : OPERATOR '(' parameter binary_operator parameter ')' optional_result { push_raw(); } optional_braces  { $$ = new bi::BinaryOperator(bi::NONE, $4, new bi::Binary($3, $5, make_loc(@$)), $7, $9, make_doc_loc(@$)); }
    ;
    
unary_operator_declaration
    : OPERATOR '(' unary_operator parameter ')' optional_result { push_raw(); } optional_braces  { $$ = new bi::UnaryOperator(bi::NONE, $3, $4, $6, $8, make_doc_loc(@$)); }
    ;

assignment_operator_declaration
    : OPERATOR LEFT_OP parameter { push_raw(); } optional_braces  { $$ = new bi::AssignmentOperator($3, $5, make_doc_loc(@$)); }
    ;

conversion_operator_declaration
    : OPERATOR result { push_raw(); } optional_braces  { $$ = new bi::ConversionOperator($2, $4, make_doc_loc(@$)); }
    ;

class_annotation
    : FINAL  { $$ = bi::FINAL; }
    |        { $$ = bi::NONE; }
    ;

class_declaration
    : class_annotation CLASS name optional_generics optional_parameters '<' base_type optional_arguments { push_raw(); } optional_class_braces  { $$ = new bi::Class($1, $3, $4, $5, $7, false, $8, $10, make_doc_loc(@$)); }
    | class_annotation CLASS name optional_generics optional_parameters { push_raw(); } optional_class_braces                                    { $$ = new bi::Class($1, $3, $4, $5, empty_type(@$), false, empty_expr(@$), $7, make_doc_loc(@$)); }
    | class_annotation CLASS name optional_generics '=' class_type { push_raw(); } ';'                                                           { $$ = new bi::Class($1, $3, $4, empty_expr(@$), $6, true, empty_expr(@$), empty_stmt(@$), make_doc_loc(@$)); }
    ;

basic_declaration
    : TYPE name '<' basic_type ';'  { push_raw(); $$ = new bi::Basic(bi::NONE, $2, $4, false, make_doc_loc(@$)); }
    | TYPE name '=' basic_type ';'  { push_raw(); $$ = new bi::Basic(bi::NONE, $2, $4, true, make_doc_loc(@$)); }
    | TYPE name ';'                 { push_raw(); $$ = new bi::Basic(bi::NONE, $2, empty_type(@$), false, make_doc_loc(@$)); }
    ;

cpp
    : CPP double_braces  { push_raw(); $$ = new bi::Raw(new bi::Name("cpp"), pop_raw(), make_loc(@$)); }
    ;

hpp
    : HPP double_braces  { push_raw(); $$ = new bi::Raw(new bi::Name("hpp"), pop_raw(), make_loc(@$)); }
    ;

assume_operator
    : '~'            { $$ = new bi::Name("~"); }
    | LEFT_QUERY_OP  { $$ = new bi::Name("<-?"); }
    ;

assume_statement
    : expression assume_operator expression ';'  { $$ = new bi::Assume($1, $2, $3, make_loc(@$)); }
    ;

expression_statement
    : expression ';'  { $$ = new bi::ExpressionStatement($1, make_loc(@$)); }
    ;

if
    : IF expression braces ELSE braces  { $$ = new bi::If($2, $3, $5, make_loc(@$)); }
    | IF expression braces ELSE if      { $$ = new bi::If($2, $3, $5, make_loc(@$)); }
    | IF expression braces              { $$ = new bi::If($2, $3, empty_stmt(@$), make_loc(@$)); }
    ;

for_annotation
    :           { $$ = bi::NONE; }
    | PARALLEL  { $$ = bi::PARALLEL; }
    ;

for_index
    : identifier
    | name ':' type  { $$ = new bi::LocalVariable(bi::NONE, $1, $3, empty_expr(@$), empty_expr(@$), empty_expr(@$), make_loc(@$)); }
    | AUTO name      { $$ = new bi::LocalVariable(bi::NONE, $2, new bi::BasicType(new bi::Name("Integer")), empty_expr(@$), empty_expr(@$), empty_expr(@$), make_loc(@$)); }
    ;

for
    : for_annotation FOR '(' for_index IN expression RANGE_OP expression ')' braces  { $$ = new bi::For($1, $4, $6, $8, $10, make_loc(@$)); }
    | for_annotation FOR for_index IN expression RANGE_OP expression braces          { $$ = new bi::For($1, $3, $5, $7, $8, make_loc(@$)); }
    ;

while
    : WHILE expression braces  { $$ = new bi::While($2, $3, make_loc(@$)); }
    ;

do_while
    : DO braces WHILE expression ';'  { $$ = new bi::DoWhile($2, $4, make_loc(@$)); }
    ;

assertion
    : ASSERT expression ';'  { $$ = new bi::Assert($2, make_loc(@$)); }
    ;

return
    : RETURN optional_expression ';'  { $$ = new bi::Return($2, make_loc(@$)); }
    ;

yield
    : YIELD expression ';'  { $$ = new bi::Yield($2, make_loc(@$)); }
    ;
    
instantiated
    : INSTANTIATED CLASS type ';'           { $$ = new bi::Instantiated<bi::Type>($3, make_loc(@$)); }
    | INSTANTIATED FUNCTION expression ';'  { $$ = new bi::Instantiated<bi::Expression>($3, make_loc(@$)); }
    | INSTANTIATED FIBER expression ';'     { $$ = new bi::Instantiated<bi::Expression>($3, make_loc(@$)); }
    ;

statement
    : local_variable_declaration
    | expression_statement
    | assume_statement
    | if
    | for
    | while
    | do_while
    | assertion
    | return
    | yield
    | cpp
    ;

statements
    : statement
    | statement statements  { $$ = new bi::StatementList($1, $2, make_loc(@$)); }
    ;

optional_statements
    : statements
    |             { $$ = empty_stmt(@$); }
    ;

class_statement
    : member_variable_declaration
    | member_function_declaration
    | member_fiber_declaration
    | assignment_operator_declaration
    | conversion_operator_declaration
    | hpp
    ;
    
class_statements
    : class_statement
    | class_statement class_statements  { $$ = new bi::StatementList($1, $2, make_loc(@$)); }
    ;
    
optional_class_statements
    : class_statements
    |                   { $$ = empty_stmt(@$); }
    ;
    
file_statement
    : global_variable_declaration
    | function_declaration
    | fiber_declaration
    | program_declaration
    | binary_operator_declaration
    | unary_operator_declaration
    | class_declaration
    | basic_declaration
    | cpp
    | hpp
    | instantiated
    ;

file_statements
    : file_statement
    | file_statement file_statements  { $$ = new bi::StatementList($1, $2, make_loc(@$)); }
    ;

optional_file_statements
    : file_statements
    |                  { $$ = empty_stmt(@$); }
    ;
    
file
    : optional_file_statements  { compiler->setRoot($1); }
    ;

result
    : RIGHT_OP type  { $$ = $2; }
    ;
    
optional_result
    : result
    |         { $$ = empty_type(@$); }
    ;

value
    : LEFT_OP expression  { $$ = $2; }
    ;
    
optional_value
    : value
    |        { $$ = empty_expr(@$); }
    ;

braces
    : '{' optional_statements '}'  { $$ = new bi::Braces($2, make_loc(@$)); }
    ;

optional_braces
    : braces
    | ';'     { $$ = empty_stmt(@$); }
    ;

class_braces
    : '{' optional_class_statements '}'  { $$ = new bi::Braces($2, make_loc(@$)); }
    ;

optional_class_braces
    : class_braces
    | ';'           { $$ = empty_stmt(@$); }
    ;
    
double_braces
    : DOUBLE_BRACE_OPEN DOUBLE_BRACE_CLOSE
    ;


/***************************************************************************
 * Types                                                                   *
 ***************************************************************************/

weak_modifier
    : '&'  { $$ = true; }
    |      { $$ = false; }
    ;

basic_type
    : name  { $$ = new bi::BasicType($1, make_loc(@$)); }
    ;

class_type
    : name optional_generic_arguments  { $$ = new bi::ClassType($1, $2, make_loc(@$)); }
    ;

unknown_type
    : name optional_generic_arguments weak_modifier  { $$ = new bi::UnknownType($3, $1, $2, make_loc(@$)); }
    ;

base_type
    : name optional_generic_arguments  { $$ = new bi::UnknownType(false, $1, $2, make_loc(@$)); }
    ;

member_type
    : unknown_type
    | member_type '.' unknown_type  { $$ = new bi::MemberType($1, $3, make_loc(@$)); }
    ;

tuple_type
    : '(' type_list ')'  { $$ = new bi::TupleType($2, make_loc(@$)); }
    ;

sequence_type
    : '[' type_list ']'  { $$ = new bi::SequenceType($2, make_loc(@$)); }
    ;

postfix_type
    : member_type
    | tuple_type
    | sequence_type
    | postfix_type '!'           { $$ = new bi::FiberType($1, make_loc(@$)); }
    | postfix_type '?'           { $$ = new bi::OptionalType($1, make_loc(@$)); }
    | postfix_type '[' size ']'  { $$ = new bi::ArrayType($1, $3, make_loc(@$)); }
    ;

function_type
    : postfix_type
    | '@' parameter_types optional_result  { $$ = new bi::FunctionType($2, $3, make_loc(@$)); }
    ;

type
    : function_type
    ;

type_list
    : type
    | type ',' type_list  { $$ = new bi::TypeList($1, $3, make_loc(@$)); }
    ;

parameter_type_list
    : type
    | type ',' parameter_type_list  { $$ = new bi::TypeList($1, $3, make_loc(@$)); }
    ;

parameter_types
    : '(' ')'                      { $$ = empty_type(@$); }
    | '(' parameter_type_list ')'  { $$ = $2; }
    ;

%%
