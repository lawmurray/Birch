%code requires {
  #include "lexer.hpp"
  #include "build/Compiler.hpp"

  extern bi::Compiler* compiler;
  extern char *yytext;
}

%code {
  #include "expression/all.hpp"
  #include "program/all.hpp"
  #include "type/all.hpp"

  #include <sstream>

  std::stringstream raw;
  
  void setloc(bi::Located* o, YYLTYPE& loc) {
    o->loc->file = compiler->file;
    o->loc->firstLine = loc.first_line;
    o->loc->lastLine = loc.last_line;
    o->loc->firstCol = loc.first_column;
    o->loc->lastCol = loc.last_column;
  }

  bi::Location* make_loc(YYLTYPE& loc) {
    return new bi::Location(compiler->file, loc.first_line, loc.last_line,
        loc.first_column, loc.last_column);
  }
  
  bi::Expression* make_empty() {
    return new bi::EmptyExpression();
  }
}

%union {
  bool valBool;
  int32_t valInt;
  double valReal;
  const char* valString;

  bi::Name* valName;
  bi::Path* valPath;
  bi::Prog* valProg;
  bi::Expression* valExpression;
  bi::Type* valType;
  bi::Statement* valStatement;
}

%token <valString> IMPORT PROG STRUCT CLASS TYPE FUNC LAMBDA COROUTINE
%token <valString> IF ELSE FOR IN WHILE RETURN CPP HPP THIS SUPER
%token <valString> DOUBLE_BRACE_OPEN DOUBLE_BRACE_CLOSE RAW NAME
%token <valBool> BOOL_LITERAL
%token <valInt> INT_LITERAL
%token <valReal> REAL_LITERAL
%token <valString> STRING_LITERAL
%token <valString> LEFT_OP RIGHT_OP LEFT_TILDE_OP RIGHT_TILDE_OP
%token <valString> AND_OP OR_OP LE_OP GE_OP EQ_OP NE_OP RANGE_OP

%type <valName> unary_operator
%type <valName> multiplicative_operator additive_operator relational_operator
%type <valName> equality_operator logical_and_operator logical_or_operator
%type <valName> stochastic_operator assignment_operator binary_operator
%type <valName> name path_name
%type <valPath> path

%type <valExpression> bool_literal int_literal real_literal string_literal
%type <valExpression> literal
%type <valExpression> value optional_value
%type <valExpression> braces optional_braces
%type <valExpression> type_braces optional_type_braces
%type <valExpression> parameter_list parameters optional_parameters
%type <valExpression> arguments optional_arguments
%type <valExpression> size_list size
%type <valExpression> var_reference var_parameter var_member_parameter var_declaration_parameter
%type <valExpression> func_reference func_parameter func_member_parameter
%type <valExpression> coroutine_parameter op_parameter conversion_parameter
%type <valProg> prog_parameter

%type <valType> type_reference struct_parameter class_parameter type_parameter
%type <valType> brackets_type parens_type primary_type lambda_type
%type <valType> type_list type
%type <valType> type_parameter_list type_parameters
%type <valType> result_type optional_result_type

%type <valExpression> reference_expression this_expression super_expression
%type <valExpression> parens_expression lambda_expression
%type <valExpression> member_expression primary_expression
%type <valExpression> index_expression index_list
%type <valExpression> brackets_expression unary_expression
%type <valExpression> multiplicative_expression additive_expression
%type <valExpression> relational_expression equality_expression
%type <valExpression> logical_and_expression logical_or_expression
%type <valExpression> stochastic_expression assignment_expression
%type <valExpression> expression_list expression

%type <valStatement> var_declaration var_member_declaration
%type <valStatement> func_declaration func_member_declaration
%type <valStatement> coroutine_declaration op_declaration conversion_declaration
%type <valStatement> type_declaration prog_declaration
%type <valStatement> import cpp hpp expression_statement if for while return
%type <valStatement> statement statements optional_statements
%type <valStatement> type_statement type_statements optional_type_statements
%type <valStatement> file_statement file_statements optional_file_statements

/*
 * left associativity on RIGHT_OP is necessary to resolve ambiguity between
 * lambda types and return types in operation declarations, e.g.:
 *
 * function x:Real + y:lambda() -> Real -> Real;
 */
%left RIGHT_OP

%locations

%start file
%%


/***************************************************************************
 * Names and paths                                                         *
 ***************************************************************************/

name
    : NAME  { $$ = new bi::Name($1, make_loc(@$)); }
    ;

path_name
    : NAME    { $$ = new bi::Name($1, make_loc(@$)); }
    ;

path
    : path_name           { $$ = new bi::Path($1, nullptr, make_loc(@$)); }
    | path_name '.' path  { $$ = new bi::Path($1, $3, make_loc(@$)); }
    ;


/***************************************************************************
 * Literals                                                                *
 ***************************************************************************/

bool_literal
    : BOOL_LITERAL  { $$ = new bi::BooleanLiteral($1, yytext, new bi::TypeReference(new bi::Name("Boolean")), make_loc(@$)); }
    ;

int_literal
    : INT_LITERAL  { $$ = new bi::IntegerLiteral($1, yytext, new bi::TypeReference(new bi::Name("Integer")), make_loc(@$)); }
    ;

real_literal
    : REAL_LITERAL  { $$ = new bi::RealLiteral($1, yytext, new bi::TypeReference(new bi::Name("Real")), make_loc(@$)); }
    ;

string_literal
    : STRING_LITERAL  { $$ = new bi::StringLiteral($1, yytext, new bi::TypeReference(new bi::Name("String")), make_loc(@$)); }
    ;

literal
    : bool_literal
    | int_literal
    | real_literal
    | string_literal
    ;
    

/***************************************************************************
 * Groupings                                                               *
 ***************************************************************************/
    
value
    : LEFT_OP expression  { $$ = $2; }
    ;
    
optional_value
    : value
    |        { $$ = make_empty(); }
    ;

braces
    : '{' optional_statements '}'  { $$ = new bi::BracesExpression($2, make_loc(@$)); }
    ;

optional_braces
    : braces
    | ';'     { $$ = make_empty(); }
    ;

type_braces
    : '{' optional_type_statements '}'  { $$ = new bi::BracesExpression($2, make_loc(@$)); }
    ;

optional_type_braces
    : type_braces
    | ';'           { $$ = make_empty(); }
    ;


/***************************************************************************
 * Raw code                                                                *
 ***************************************************************************/

raw
    : RAW      { raw << $1; }
    | raw RAW  { raw << $2; }
    ;
    
double_braces
    : DOUBLE_BRACE_OPEN { raw.str(""); } raw DOUBLE_BRACE_CLOSE
    ;


/***************************************************************************
 * Parameters                                                              *
 ***************************************************************************/

var_parameter
    : name ':' type  { $$ = new bi::VarParameter($1, $3, bi::PARAMETER_FORM, make_empty(), make_empty(), make_loc(@$)); }
    | ':' type       { $$ = new bi::VarParameter(new bi::Name(), $2, bi::PARAMETER_FORM, make_empty(), make_empty(), make_loc(@$)); }
    ;

var_member_parameter
    : name ':' type optional_arguments optional_value  { $$ = new bi::VarParameter($1, $3, bi::MEMBER_VARIABLE_FORM, $4, $5, make_loc(@$)); }
    ;

var_declaration_parameter
    : name ':' type optional_arguments optional_value  { $$ = new bi::VarParameter($1, $3, bi::VARIABLE_FORM, $4, $5, make_loc(@$)); }
    ;
    
func_parameter
    : name parameters optional_result_type optional_braces  { $$ = new bi::FuncParameter($1, $2, $3, $4, bi::FUNCTION_FORM, make_loc(@$)); }
    ;

func_member_parameter
    : name parameters optional_result_type optional_braces  { $$ = new bi::FuncParameter($1, $2, $3, $4, bi::MEMBER_FUNCTION_FORM, make_loc(@$)); }
    ;

coroutine_parameter
    : name parameters optional_result_type optional_braces  { $$ = new bi::FuncParameter($1, $2, $3, $4, bi::COROUTINE_FORM, make_loc(@$)); }
    ;
    
op_parameter
    : var_parameter assignment_operator var_parameter optional_braces                   { $$ = new bi::FuncParameter($1, $2, $3, new bi::EmptyType(), $4, bi::ASSIGN_FORM, make_loc(@$)); }
    | var_parameter binary_operator var_parameter optional_result_type optional_braces  { $$ = new bi::FuncParameter($1, $2, $3, $4, $5, bi::BINARY_FORM, make_loc(@$)); }
    | unary_operator var_parameter optional_result_type optional_braces                 { $$ = new bi::FuncParameter($1, $2, $3, $4, bi::UNARY_FORM, make_loc(@$)); }
    ;

conversion_parameter
    : result_type optional_braces  { $$ = new bi::ConversionParameter($1, $2, make_loc(@$)); }
    ;

struct_parameter
    : name optional_parameters '<' type_reference optional_arguments optional_type_braces  { $$ = new bi::TypeParameter($1, $2, $4, $5, $6, bi::STRUCT_TYPE, make_loc(@$)); }
    | name optional_parameters optional_type_braces                                        { $$ = new bi::TypeParameter($1, $2, new bi::EmptyType(), make_empty(), $3, bi::STRUCT_TYPE, make_loc(@$)); }
    ;

class_parameter
    : name optional_parameters '<' type_reference optional_arguments optional_type_braces  { $$ = new bi::TypeParameter($1, $2, $4, $5, $6, bi::CLASS_TYPE, make_loc(@$)); }
    | name optional_parameters optional_type_braces                     { $$ = new bi::TypeParameter($1, $2, new bi::EmptyType(), make_empty(), $3, bi::CLASS_TYPE, make_loc(@$)); }
    ;

type_parameter
    : name '=' type_reference ';'  { $$ = new bi::TypeParameter($1, make_empty(), $3, make_empty(), make_empty(), bi::ALIAS_TYPE, make_loc(@$)); }
    | name ';'                     { $$ = new bi::TypeParameter($1, make_empty(), new bi::EmptyType(), make_empty(), make_empty(), bi::BUILTIN_TYPE, make_loc(@$)); }
    ;

prog_parameter
    : name parameters optional_braces  { $$ = new bi::ProgParameter($1, $2, $3, make_loc(@$)); }
    ;

parameter_list
    : var_declaration_parameter
    | var_declaration_parameter ',' parameter_list  { $$ = new bi::ExpressionList($1, $3, make_loc(@$)); }
    ;

parameters
    : '(' ')'                 { $$ = make_empty(); }
    | '(' parameter_list ')'  { $$ = $2; }
    ;

optional_parameters
    : parameters
    |             { $$ = make_empty(); }

type_parameter_list
    : type
    | type ',' type_parameter_list  { $$ = new bi::TypeList($1, $3, make_loc(@$)); }
    ;

type_parameters
    : '(' ')'                      { $$ = new bi::EmptyType(); }
    | '(' type_parameter_list ')'  { $$ = $2; }
    ;
    ;

result_type
    : RIGHT_OP type  { $$ = $2; }
    ;
    
optional_result_type
    : result_type
    |             %prec RIGHT_OP { $$ = new bi::EmptyType(); }
    ;


/***************************************************************************
 * References                                                              *
 ***************************************************************************/

var_reference
    : name  { $$ = new bi::VarReference($1, make_loc(@$)); }
    ;

func_reference
    : name arguments  { $$ = new bi::FuncReference($1, $2, make_loc(@$)); }
    ;

type_reference
    : name  { $$ = new bi::TypeReference($1, make_loc(@$)); }
    ;

arguments
    : '(' ')'                  { $$ = make_empty(); }
    | '(' expression_list ')'  { $$ = $2; }
    ;

optional_arguments
    : arguments
    |            { $$ = make_empty(); }
    ;

size
    : expression  { $$ = new bi::Span($1, make_loc(@$)); }
    | '_'         { $$ = new bi::Span(make_empty(), make_loc(@$)); }
    ;

size_list
    : size
    | size ',' size_list  { $$ = new bi::ExpressionList($1, $3, make_loc(@$)); }
    ;


/***************************************************************************
 * Types                                                                   *
 ***************************************************************************/

parens_type
    : '(' type_list ')'  { $$ = new bi::ParenthesesType($2, make_loc(@$)); }
    ;

primary_type
    : type_reference
    | parens_type
    ;

brackets_type
    : primary_type
    | primary_type '[' size_list ']'  { $$ = new bi::BracketsType($1, $3, make_loc(@$)); }
    ;

lambda_type
    : brackets_type
    | LAMBDA type_parameters optional_result_type  { $$ = new bi::LambdaType($2, $3, make_loc(@$)); }
    ;

type_list
    : lambda_type
    | lambda_type ',' type_list  { $$ = new bi::TypeList($1, $3, make_loc(@$)); }
    ;

type
    : lambda_type
    ;


/***************************************************************************
 * Expressions                                                             *
 ***************************************************************************/

reference_expression
    : var_reference
    | func_reference
    ;

parens_expression
    : '(' expression_list ')'  { $$ = new bi::ParenthesesExpression($2, make_loc(@$)); }
    ;
    
lambda_expression
    : LAMBDA parameters optional_result_type optional_braces  { $$ = new bi::FuncParameter(new bi::Name(), $2, $3, $4, bi::LAMBDA_FORM, make_loc(@$)); }
    ;

this_expression
    : THIS  { $$ = new bi::This(make_loc(@$)); }
    ;

super_expression
    : SUPER  { $$ = new bi::Super(make_loc(@$)); }
    ;

primary_expression
    : literal
    | parens_expression
    | lambda_expression
    | reference_expression
    | this_expression
    | super_expression
    ;

index_expression
    : expression RANGE_OP expression  { $$ = new bi::Range($1, $3, make_loc(@$)); }
    | expression                      { $$ = new bi::Index($1, make_loc(@$)); }
    ;
    
index_list
    : index_expression
    | index_expression ',' index_list  { $$ = new bi::ExpressionList($1, $3, make_loc(@$)); }
    ;

brackets_expression
    : primary_expression
    | primary_expression '[' index_list ']'  { $$ = new bi::BracketsExpression($1, $3, make_loc(@$)); }
    ;

member_operator
    : '.'
    ;

member_expression
    : brackets_expression
    | member_expression member_operator brackets_expression  { $$ = new bi::Member($1, $3, make_loc(@$)); }
    ;

unary_operator
    : '+'  { $$ = new bi::Name("+", make_loc(@$)); }
    | '-'  { $$ = new bi::Name("-", make_loc(@$)); }
    | '!'  { $$ = new bi::Name("!", make_loc(@$)); }
    ;

unary_expression
    : member_expression
    | unary_operator member_expression  { $$ = new bi::FuncReference($1, $2, make_loc(@$)); }
    ;

multiplicative_operator
    : '*'   { $$ = new bi::Name("*", make_loc(@$)); }
    | '/'   { $$ = new bi::Name("/", make_loc(@$)); }
    | '\\'  { $$ = new bi::Name("\\", make_loc(@$)); }
    ;

multiplicative_expression
    : unary_expression
    | multiplicative_expression multiplicative_operator unary_expression  { $$ = new bi::FuncReference($1, $2, $3, make_loc(@$)); }
    ;

additive_operator
    : '+'  { $$ = new bi::Name("+", make_loc(@$)); }
    | '-'  { $$ = new bi::Name("-", make_loc(@$)); }
    ;

additive_expression
    : multiplicative_expression
    | additive_expression additive_operator multiplicative_expression  { $$ = new bi::FuncReference($1, $2, $3, make_loc(@$)); }
    ;

relational_operator
    : '<'    { $$ = new bi::Name("<", make_loc(@$)); }
    | '>'    { $$ = new bi::Name(">", make_loc(@$)); }
    | LE_OP  { $$ = new bi::Name("<=", make_loc(@$)); }
    | GE_OP  { $$ = new bi::Name(">=", make_loc(@$)); }
    ;
    
relational_expression
    : additive_expression
    | relational_expression relational_operator additive_expression  { $$ = new bi::FuncReference($1, $2, $3, make_loc(@$)); }
    ;

equality_operator
    : EQ_OP  { $$ = new bi::Name("==", make_loc(@$)); }
    | NE_OP  { $$ = new bi::Name("!=", make_loc(@$)); }
    ;

equality_expression
    : relational_expression
    | equality_expression equality_operator relational_expression  { $$ = new bi::FuncReference($1, $2, $3, make_loc(@$)); }
    ;

logical_and_operator
    : AND_OP  { $$ = new bi::Name("&&", make_loc(@$)); }
    ;

logical_and_expression
    : equality_expression
    | logical_and_expression logical_and_operator equality_expression  { $$ = new bi::FuncReference($1, $2, $3, make_loc(@$)); }
    ;

logical_or_operator
    : OR_OP  { $$ = new bi::Name("||", make_loc(@$)); }
    ;

logical_or_expression
    : logical_and_expression
    | logical_or_expression logical_or_operator logical_and_expression  { $$ = new bi::FuncReference($1, $2, $3, make_loc(@$)); }
    ;

stochastic_operator
    : RIGHT_TILDE_OP  { $$ = new bi::Name("~>", make_loc(@$)); }
    ;

stochastic_expression
    : logical_or_expression
    | stochastic_expression stochastic_operator logical_or_expression  { $$ = new bi::FuncReference($1, $2, $3, make_loc(@$)); }
    ;

assignment_operator
    : LEFT_OP        { $$ = new bi::Name("<-", make_loc(@$)); }
    | LEFT_TILDE_OP  { $$ = new bi::Name("<~", make_loc(@$)); }
    | '~'            { $$ = new bi::Name("~", make_loc(@$)); }
    ;
    
assignment_expression
    : stochastic_expression
    | stochastic_expression assignment_operator assignment_expression  { $$ = new bi::FuncReference($1, $2, $3, make_loc(@$)); }
    ;

expression_list
    : assignment_expression
    | assignment_expression ',' expression_list  { $$ = new bi::ExpressionList($1, $3, make_loc(@$)); }
    ;

expression
    : assignment_expression
    ;

binary_operator
    : multiplicative_operator
    | additive_operator
    | relational_operator
    | equality_operator
    | logical_and_operator
    | logical_or_operator
    | stochastic_operator
    ;


/***************************************************************************
 * Statements                                                              *
 ***************************************************************************/

var_declaration
    : var_declaration_parameter ';'  { $$ = new bi::VarDeclaration(dynamic_cast<bi::VarParameter*>($1), make_loc(@$)); }
    ;

var_member_declaration
    : var_member_parameter ';'  { $$ = new bi::VarDeclaration(dynamic_cast<bi::VarParameter*>($1), make_loc(@$)); }
    ;

func_declaration
    : FUNC func_parameter  { $$ = new bi::FuncDeclaration(dynamic_cast<bi::FuncParameter*>($2), make_loc(@$)); }
    ;

func_member_declaration
    : FUNC func_member_parameter  { $$ = new bi::FuncDeclaration(dynamic_cast<bi::FuncParameter*>($2), make_loc(@$)); }
    ;

coroutine_declaration
    : COROUTINE coroutine_parameter  { $$ = new bi::FuncDeclaration(dynamic_cast<bi::FuncParameter*>($2), make_loc(@$)); }
    ;

op_declaration
    : FUNC op_parameter  { $$ = new bi::FuncDeclaration(dynamic_cast<bi::FuncParameter*>($2), make_loc(@$)); }
    ;

conversion_declaration
    : FUNC conversion_parameter  { $$ = new bi::ConversionDeclaration(dynamic_cast<bi::ConversionParameter*>($2), make_loc(@$)); }
    ;

type_declaration
    : STRUCT struct_parameter  { $$ = new bi::TypeDeclaration(dynamic_cast<bi::TypeParameter*>($2), make_loc(@$)); } 
    | CLASS class_parameter    { $$ = new bi::TypeDeclaration(dynamic_cast<bi::TypeParameter*>($2), make_loc(@$)); } 
    | TYPE type_parameter      { $$ = new bi::TypeDeclaration(dynamic_cast<bi::TypeParameter*>($2), make_loc(@$)); } 
    ;
    
prog_declaration
    : PROG prog_parameter  { $$ = new bi::ProgDeclaration(dynamic_cast<bi::ProgParameter*>($2), make_loc(@$)); } 
    ;

expression_statement
    : expression ';'  { $$ = new bi::ExpressionStatement($1, make_loc(@$)); }
    ;

if
    : IF '(' expression ')' braces ELSE braces  { $$ = new bi::If($3, $5, $7, make_loc(@$)); }
    | IF '(' expression ')' braces ELSE if      { $$ = new bi::If($3, $5, new bi::BracesExpression($7), make_loc(@$)); }
    | IF '(' expression ')' braces              { $$ = new bi::If($3, $5, make_empty(), make_loc(@$)); }
    ;

for
    : FOR '(' var_reference IN expression RANGE_OP expression ')' braces  { $$ = new bi::For($3, $5, $7, $9, make_loc(@$)); }
    ;

while
    : WHILE '(' expression ')' braces  { $$ = new bi::While($3, $5, make_loc(@$)); }
    ;

return
    : RETURN expression ';'  { $$ = new bi::Return($2, make_loc(@$)); }
    ;

cpp
    : CPP double_braces  { $$ = new bi::Raw(new bi::Name("cpp"), raw.str(), make_loc(@$)); }
    ;

hpp
    : HPP double_braces  { $$ = new bi::Raw(new bi::Name("hpp"), raw.str(), make_loc(@$)); }
    ;

statement
    : expression_statement
    | var_declaration
    | if
    | for
    | while
    | return
    | cpp
    | hpp
    ;

statements
    : statement
    | statement statements  { $$ = new bi::StatementList($1, $2, make_loc(@$)); }
    ;

optional_statements
    : statements
    |             { $$ = new bi::EmptyStatement(); }
    ;

type_statement
    : var_member_declaration
    | func_member_declaration
    | conversion_declaration
    | hpp
    ;
    
type_statements
    : type_statement
    | type_statement type_statements  { $$ = new bi::StatementList($1, $2, make_loc(@$)); }
    ;
    
optional_type_statements
    : type_statements
    |                   { $$ = new bi::EmptyStatement(); }
    ;

import
    : IMPORT path ';'  { $$ = new bi::Import($2, compiler->import($2), make_loc(@$)); }
    ;
    
file_statement
    : import
    | cpp
    | hpp
    | var_declaration
    | func_declaration
    | coroutine_declaration
    | op_declaration
    | prog_declaration
    | type_declaration
    ;

file_statements
    : file_statement
    | file_statement file_statements  { $$ = new bi::StatementList($1, $2, make_loc(@$)); }
    ;

optional_file_statements
    : file_statements
    |                  { $$ = new bi::EmptyStatement(); }
    ;
    
file
    : optional_file_statements  { compiler->setRoot($1); }
    ;
    
%%
