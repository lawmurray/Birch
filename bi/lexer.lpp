%{
#include "parser.hpp"
#include "lexer.hpp"
#include "build/Compiler.hpp"

/* Flex uses the register keyword, this is deprecated in C++11, and removed
 * in C++17, so define it away */
#define register

#define YY_USER_ACTION yycount();

extern bi::Compiler* compiler;
int yyline = 1, yycol = 1;

void yyerror(const char *msg) {
  yylocation();
  std::cerr << msg << '\n';
  exit(-1);
}

void yywarn(const char *msg) {
  yylocation();
  std::cerr << "warning: " << msg << '\n';
}

void yylocation() {
  /* the format here matches that of g++ and clang++ such that Eclipse,
   * when parsing the error output, is able to annotate lines within the
   * editor */
  if (compiler->file) {
    std::cerr << compiler->file->path;
    std::cerr << ':' << yylloc.first_line;
    std::cerr << ':' << yylloc.first_column;
    std::cerr << ": ";
  }
}

void yycount() {
  yylloc.first_line = yyline;
  yylloc.first_column = yycol;

  for (int i = 0; yytext[i] != '\0'; ++i) {
    if (yytext[i] == '\n') {
      ++yyline;
      yycol = 1;
    } else if (yytext[i] == '\t') {
      yycol += 8 - (yycol % 8);
    } else {
      ++yycol;
    }
  }

  yylloc.last_line = yyline;
  yylloc.last_column = yycol;
}

void yyreset() {
  yyline = 1;
  yycol = 1;
}

%}

%option noyywrap nounput noinput

%x COMMENT_EOL COMMENT_INLINE COMMENT_DOC DOUBLE_BRACE

D                                  [0-9]
L                                  [a-zA-Z]
U                                  _
G                                  (α|β|γ|δ|ε|ζ|η|θ|ι|κ|λ|μ|ν|ξ|ο|π|ρ|σ|τ|υ|φ|χ|ψ|ω|Γ|Δ|Θ|Λ|Ξ|Π|Σ|Υ|Φ|Ψ|Ω)
H                                  [a-fA-F0-9]
E                                  [Ee][+-]?{D}+

%%

[ \t\n\r\f]

"//"                                { BEGIN(COMMENT_EOL); }
<COMMENT_EOL>"\n"                   { BEGIN(INITIAL); }
<COMMENT_EOL>.                      { }

"/*"                                { BEGIN(COMMENT_INLINE); }
<COMMENT_INLINE>"*/"                { BEGIN(INITIAL); }
<COMMENT_INLINE>"\n"                { }
<COMMENT_INLINE>.                   { }

"/**"                               { BEGIN(COMMENT_DOC); raw.str(""); }
<COMMENT_DOC>"*/"                   { BEGIN(INITIAL); }
<COMMENT_DOC>"\n"                   { raw << yytext; }
<COMMENT_DOC>.                      { raw << yytext; }

"{{"                                { BEGIN(DOUBLE_BRACE); raw.str(""); return DOUBLE_BRACE_OPEN; }
<DOUBLE_BRACE>"}}"                  { BEGIN(INITIAL); return DOUBLE_BRACE_CLOSE; }
<DOUBLE_BRACE>"\n"                  { raw << yytext; }
<DOUBLE_BRACE>.                     { raw << yytext; }

"function"                          { return FUNCTION; }
"fiber"                             { return FIBER; }
"program"                           { return PROGRAM; }
"class"                             { return CLASS; }
"type"                              { return TYPE; }
"operator"                          { return OPERATOR; }
"auto"                              { return AUTO; }
"instantiated"                      { return INSTANTIATED; }
"if"                                { return IF; }
"else"                              { return ELSE; }
"for"                               { return FOR; }
"in"                                { return IN; }
"while"                             { return WHILE; }
"do"                                { return DO; }
"assert"                            { return ASSERT; }
"return"                            { return RETURN; }
"yield"                             { return YIELD; }
"cpp"                               { return CPP; }
"hpp"                               { return HPP; }
"this"                              { return THIS; }
"super"                             { return SUPER; }
"global"                            { return GLOBAL; }
"parallel"                          { return PARALLEL; }
"dynamic"                           { return DYNAMIC; }
"final"                             { return FINAL; }
"abstract"                          { return ABSTRACT; }
"nil"                               { return NIL; }

"true"                              { yylval.valString = "true"; return BOOL_LITERAL; }
"false"                             { yylval.valString = "false"; return BOOL_LITERAL; }

({L}|{G})({L}|{G}|{U}|{D})*'*       { yylval.valString = strdup(yytext); return NAME; }

{D}+{E}                             { yylval.valString = strdup(yytext); return REAL_LITERAL; }
{D}+\.{D}+({E})?                    { yylval.valString = strdup(yytext); return REAL_LITERAL; }
0[xX]{H}+                           { yylval.valString = strdup(yytext); return INT_LITERAL; }
0{D}+                               { yylval.valString = strdup(yytext); return INT_LITERAL; }
{D}+                                { yylval.valString = strdup(yytext); return INT_LITERAL; }
\"(\\\"|[^\"\n\r\f])*\"             { yylval.valString = strdup(yytext); return STRING_LITERAL; }

"<-"                                { return LEFT_OP; } 
"->"                                { return RIGHT_OP; }
"<~"                                { return LEFT_TILDE_OP; }
"~>"                                { return RIGHT_TILDE_OP; }
"~"                                 { return '~'; }
"<-?"                               { return LEFT_QUERY_OP; }
".."                                { return RANGE_OP; }
"?"                                 { return '?'; }
"!"                                 { return '!'; }
"&"                                 { return '&'; }
"&&"                                { return AND_OP; }
"||"                                { return OR_OP; }
"<"                                 { return '<'; }
">"                                 { return '>'; }
"<="                                { return LE_OP; }
">="                                { return GE_OP; }
"=="                                { return EQ_OP; }
"!="                                { return NE_OP; }
"+"                                 { return '+'; }
"-"                                 { return '-'; }
"*"                                 { return '*'; }
"/"                                 { return '/'; }
";"                                 { return ';'; }
"{"                                 { return '{'; }
"}"                                 { return '}'; }
","                                 { return ','; }
":"                                 { return ':'; }
"="                                 { return '='; }
"("                                 { return '('; }
")"                                 { return ')'; }
"["                                 { return '['; }
"]"                                 { return ']'; }
"."                                 { return '.'; }
"_"                                 { return '_'; }
"@"                                 { return '@'; }
.                                   { yyerror("syntax error"); }

%%
