/**
 * Delta distribution, representing a distribution on a discrete space with
 * all probability mass at one location.
 *
 * @param μ Location.
 */
final class DeltaDistribution<Arg>(μ:Arg) < Distribution<Integer> {
  /**
   * Location.
   */
  μ:Arg <- μ;

  override function simulate() -> Integer! {
    return value(μ);
  }

  override function simulateLazy() -> Integer! {
    return eval(μ);
  }

  override function logpdf(x:Integer!) -> Real! {
    return where(x == value(μ), 0.0, -inf);
  }

  override function logpdfLazy(x:Integer!) -> Real! {
    return where(x == eval(μ), 0.0, -inf);
  }

  override function hoist() -> Expression<Real> {
    return box(where(random() == μ, 0.0, -inf));
  }

  override function lower() -> Integer!? {
    return value(μ);
  }
  
  override function upper() -> Integer!? {
    return value(μ);
  }

  override function fix() {
    super.fix();
    constant(μ);
  }

  override function isBoundedDiscrete() -> Boolean {
    return !x?;
  }

  override function getBoundedDiscrete() -> (Expression<Real[_]>, Integer!)? {
    assert isBoundedDiscrete();
    return (box(vector(1.0, 1)), value(μ));
  }

  override function write(buffer:Buffer) {
    buffer.set("class", "Delta");
    buffer.set("μ", value(μ));
  }
}

/**
 * Create delta distribution.
 */
function Delta<Arg>(μ:Arg) -> Distribution<Integer> {
  return make_delta(μ);
}
function make_delta<Arg>(μ:Arg) -> Distribution<Integer> {
  return peg_delta(peg(μ));
}
function peg_delta<Arg>(μ:Arg) -> Distribution<Integer> {
  return DeltaDistribution<Arg>(μ);
}
