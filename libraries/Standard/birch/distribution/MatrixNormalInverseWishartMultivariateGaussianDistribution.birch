/**
 * Multivariate Gaussian distribution with linear transformation of
 * matrix-normal-inverse-Wishart prior.
 */
class MatrixNormalInverseWishartMultivariateGaussianDistribution<Arg1,Arg2,
    Arg3,Arg4,Arg5,Arg6,Arg7>(a:Arg1, N:Arg2, Λ:Arg3, Γ:Arg4, k:Arg5, c:Arg6,
    φ2:Arg7) < Distribution<Real[_]> {
  /**
   * Scale.
   */
  a:Arg1 <- a;

  /**
   * Among-row concentration times mean.
   */
  N:Arg2 <- N;

  /**
   * Among-row conentration.
   */
  Λ:Arg3 <- Λ;
  
  /**
   * Accumulator for among-column spread.
   */
  Γ:Arg4 <- Γ;

  /**
   * Degrees of freedom.
   */
  k:Arg5 <- k;

  /**
   * Offset.
   */
  c:Arg6 <- c;

  /**
   * Additional variance scale.
   */
  φ2:Arg7 <- φ2;

  override function supportsJoin() -> Boolean {
    return true;
  }

  override function simulate() -> Real[_] {
    if !this.joined? {
      return value(simulate_matrix_normal_inverse_wishart_multivariate_gaussian(
          a, N, Λ, Γ, k, c, φ2));
    } else {
      let (Y, A, N, Λ, Γ, k, Φ) <-
          this.joined!.joinMatrixNormalInverseWishartMultivariateGaussian()!;
      let p <- update_matrix_normal_inverse_wishart_matrix_gaussian(
          Y, A, N, Λ, Γ, k, Φ);
      return value(simulate_matrix_normal_inverse_wishart_multivariate_gaussian(
          this.a, p.N, p.Λ, p.Γ, p.k, this.c, this.φ2));
    }
  }

  override function simulateLazy() -> Real[_] {
    if !this.joined? {
      return eval(simulate_matrix_normal_inverse_wishart_multivariate_gaussian(
          a, N, Λ, Γ, k, c, φ2));
    } else {
      let (Y, A, N, Λ, Γ, k, Φ) <-
          this.joined!.joinMatrixNormalInverseWishartMultivariateGaussian()!;
      let p <- update_matrix_normal_inverse_wishart_matrix_gaussian(
          Y, A, N, Λ, Γ, k, Φ);
      return eval(simulate_matrix_normal_inverse_wishart_multivariate_gaussian(
          this.a, p.N, p.Λ, p.Γ, p.k, this.c, this.φ2));
    }
  }

  override function logpdf(x:Real[_]) -> Real! {
    if !this.joined? {
      return value(logpdf_matrix_normal_inverse_wishart_multivariate_gaussian(
          x, a, N, Λ, Γ, k, c, φ2));
    } else {
      let (Y, A, N, Λ, Γ, k, Φ) <-
          this.joined!.joinMatrixNormalInverseWishartMultivariateGaussian()!;
      let p <- update_matrix_normal_inverse_wishart_matrix_gaussian(
          Y, A, N, Λ, Γ, k, Φ);
      return value(logpdf_matrix_normal_inverse_wishart_multivariate_gaussian(
          x, this.a, p.N, p.Λ, p.Γ, p.k, this.c, this.φ2));
    }
  }

  override function logpdfLazy(x:Real[_]) -> Real! {
    if !this.joined? {
      return eval(logpdf_matrix_normal_inverse_wishart_multivariate_gaussian(
          x, a, N, Λ, Γ, k, c, φ2));
    } else {
      let (Y, A, N, Λ, Γ, k, Φ) <-
          this.joined!.joinMatrixNormalInverseWishartMultivariateGaussian()!;
      let p <- update_matrix_normal_inverse_wishart_matrix_gaussian(
          Y, A, N, Λ, Γ, k, Φ);
      return eval(logpdf_matrix_normal_inverse_wishart_multivariate_gaussian(
          x, this.a, p.N, p.Λ, p.Γ, p.k, this.c, this.φ2));
    }
  }

  override function update(x:Real[_]) -> Delay? {
    if !this.joined? {
      return update_matrix_normal_inverse_wishart_multivariate_gaussian(
          x, a, N, Λ, Γ, k, c, φ2);
    } else {
      let (Y, A, N, Λ, Γ, k, Φ) <-
          joinMatrixNormalInverseWishartMultivariateGaussian()!;
      return update_matrix_normal_inverse_wishart_matrix_gaussian(
          Y, A, N, Λ, Γ, k, Φ);
    }
  }

  override function updateLazy(x:Random<Real[_]>) -> Delay? {
    if !this.joined? {
      return update_matrix_normal_inverse_wishart_multivariate_gaussian(
          x, a, N, Λ, Γ, k, c, φ2);
    } else {
      let (Y, A, N, Λ, Γ, k, Φ) <-
          joinMatrixNormalInverseWishartMultivariateGaussian()!;
      return update_matrix_normal_inverse_wishart_matrix_gaussian(
          Y, A, N, Λ, Γ, k, Φ);
    }
  }

  override function hoist() -> Expression<Real> {
    let (Y, A, N, Λ, Γ, k, Φ) <-
        joinMatrixNormalInverseWishartMultivariateGaussian()!;
    return box(logpdf_matrix_normal_inverse_wishart_matrix_gaussian(
        Y, A, N, Λ, Γ, k, Φ));
  }

  override function fix() {
    super.fix();
    constant(a);
    constant(N);
    constant(Λ);
    constant(Γ);
    constant(k);
    constant(c);
    constant(φ2);

    if hasJoined() {
      let (Y, A, N, Λ, Γ, k, Φ) <-
          getJoined().joinMatrixNormalInverseWishartMultivariateGaussian()!;
      let p <- update_matrix_normal_inverse_wishart_matrix_gaussian(
          value(Y), value(A), value(N), value(Λ), value(Γ), value(k),
          value(Φ));
      setUpdated(make_matrix_normal_inverse_wishart_multivariate_gaussian(
          this.a, p.N, p.Λ, p.Γ, p.k, this.c, this.φ2));
      setJoined(nil);
    }
  }

  override function getMatrixNormalInverseWishartMultivariateGaussian(
      x:Random<Real[_]>) -> (Expression<Real[_]>, Expression<Real[_]>,
      Expression<Real[_,_]>, Expression<Real[_,_]>, Expression<Real[_,_]>,
      Expression<Real>, Expression<Real>)? {
    return box(x - c, a, N, Λ, Γ, k, φ2);
  }

  override function joinMatrixNormalInverseWishartMultivariateGaussian() ->
      (Expression<Real[_,_]>, Expression<Real[_,_]>, Expression<Real[_,_]>,
      Expression<Real[_,_]>, Expression<Real[_,_]>, Expression<Real>,
      Expression<Real[_,_]>)? {
    Y:PackExpression<Expression<Real[_]>,Real>;
    A:PackExpression<Expression<Real[_]>,Real>;
    φ2:StackExpression<Expression<Real>,Real>;
    p:Distribution<Real[_]> <- this;
    while true {
      let x' <- p.random();
      let (y', a', N', Λ', Γ', k', φ2') <-
          p.getMatrixNormalInverseWishartMultivariateGaussian(x')!;
      Y.push(y');
      A.push(a');
      φ2.push(φ2');
      if p.hasJoined() && p.getJoined().isSubordinate() {
        p <- Distribution<Real[_]>?(p.getJoined())!;
      } else {
        return (box(transpose(Y)), box(transpose(A)), N', Λ', Γ', k',
            box(diagonal(φ2)));
      }
    }
  }
}

function MatrixNormalInverseWishartMultivariateGaussian<Arg1,Arg2,Arg3,Arg4,
    Arg5,Arg6,Arg7>(a:Arg1, M:Arg2, Σ:Arg3, Ψ:Arg4, k:Arg5, c:Arg6,
    φ2:Arg7) -> Distribution<Real[_]> {
  let L <- chol(Σ);
  let O <- trisolve(L, M);
  let N <- triinnersolve(L, O);
  let Λ <- cholinv(L);
  let Γ <- Ψ + inner(O);
  return make_matrix_normal_inverse_wishart_multivariate_gaussian(a, N, Λ, Γ,
      k, c, φ2);
}
function make_matrix_normal_inverse_wishart_multivariate_gaussian<Arg1,Arg2,
    Arg3,Arg4,Arg5,Arg6,Arg7>(a:Arg1, N:Arg2, Λ:Arg3, Γ:Arg4, k:Arg5, c:Arg6,
    φ2:Arg7) -> {
  return peg_matrix_normal_inverse_wishart_multivariate_gaussian(
      peg(a), peg(N), peg(Λ), peg(Γ), peg(k),
      peg(c), peg(φ2));
}
function peg_matrix_normal_inverse_wishart_multivariate_gaussian<Arg1,Arg2,
    Arg3,Arg4,Arg5,Arg6,Arg7>(a:Arg1, N:Arg2, Λ:Arg3, Γ:Arg4, k:Arg5, c:Arg6,
    φ2:Arg7) -> {
  return MatrixNormalInverseWishartMultivariateGaussianDistribution<
      Arg1,Arg2,Arg3,Arg4,Arg5,Arg6,Arg7>(a, N, Λ, Γ, k, c, φ2);
}

/**
 * Create multivariate Gaussian distribution.
 */
function MultivariateGaussian<Arg1,Arg2,Arg3>(μ:Add<Inner<Random<Real[_,_]>,
    Arg1>,Arg2>, Σ:Mul<Arg3,Random<Real[_,_]>>) -> Distribution<Real[_]> {
  let m <- μ.l().l();
  let A <- μ.l().r();
  let c <- μ.r();
  let ω2 <- Σ.l();
  let Ω <- Σ.r();
  if Ω.isInverseWishart() && m.isMatrixNormalInverseWishart() &&
      Ω.hasMarginalized() && Ω.getMarginalized().getUpdated() ==
      m.getDistribution().getUpdated() {
    let p_Ω <- Ω.getDistribution();
    let p_m <- m.graft();
    let (N, Λ1, Γ, k) <- p_m.getMatrixNormalInverseWishart()!;
    let p <- make_matrix_normal_inverse_wishart_multivariate_gaussian(A, N,
        Λ1, Γ, k, c, ω2);
    if p_m.hasJoined() {
      p.setJoined(p_m.getJoined());  // carry forward
      p_m.setJoined(nil);
    }
    p_Ω.setSecondOrder(true);
    p_m.setMarginalized(p);
    return p;
  } else if Ω.isInverseWishart() {
    let p_Ω <- Ω.graft();
    let (Ψ, k) <- p_Ω.getInverseWishart()!;
    let ν <- μ/ω2;
    let λ <- 1.0/ω2;
    let Γ <- Ψ + outer(μ/sqrt(ω2));
    let p <- make_multivariate_normal_inverse_wishart(ν, λ, Γ, k);
    p_Ω.setMarginalized(p);
    return p;
  } else {
    return make_multivariate_gaussian(μ, Σ);
  }
}

/**
 * Create multivariate Gaussian distribution.
 */
function MultivariateGaussian<Arg1,Arg2>(μ:Inner<Random<Real[_,_]>,Arg1>,
    Σ:Mul<Arg2,Random<Real[_,_]>>) -> Distribution<Real[_]> {
  return MultivariateGaussian(μ + 0.0, Σ);
}

/**
 * Create multivariate Gaussian distribution.
 */
function MultivariateGaussian<Arg1,Arg2>(μ:Add<Inner<Random<Real[_,_]>,
    Arg1>,Arg2>, Σ:Random<Real[_,_]>) -> Distribution<Real[_]> {
  return MultivariateGaussian(μ, 1.0*Σ);
}

/**
 * Create multivariate Gaussian distribution.
 */
function MultivariateGaussian<Arg>(μ:Inner<Random<Real[_,_]>,Arg>,
    Σ:Random<Real[_,_]>) -> Distribution<Real[_]> {
  return MultivariateGaussian(μ + 0.0, 1.0*Σ);
}

/*
 * Simulate a matrix-normal-inverse-Wishart-multivariate-Gaussian
 * distribution.
 *
 * @param a Scale.
 * @param N Among-row concentration times mean.
 * @param Λ Among-row concentration.
 * @param Γ Accumulator for among-column spread.
 * @param k Degrees of freedom.
 * @param c Offset.
 * @param φ2 Variance scale.
 *
 * @return the variate.
 */
function simulate_matrix_normal_inverse_wishart_multivariate_gaussian<
    Arg1,Arg2,Arg3,Arg4,Arg5,Arg6,Arg7>(a:Arg1, N:Arg2, Λ:Arg3, Γ:Arg4,
    k:Arg5, c:Arg6, φ2:Arg7) -> {
  let L <- chol(Λ);
  let O <- trisolve(L, N);
  let M <- triinnersolve(L, O);
  let Ψ <- Γ - inner(O);
  let Σ <- (φ2 + dot(trisolve(L, a)))*Ψ;
  return simulate_multivariate_t(k, inner(M, a) + c, Σ);
}

/*
 * Observe a multivariate $t$-distribution variate with location and scale.
 *
 * @param x The variate.
 * @param a Scale.
 * @param N Among-row concentration times mean.
 * @param Λ Among-row concentration.
 * @param Γ Accumulator for among-column spread.
 * @param k Degrees of freedom.
 * @param c Offset.
 * @param φ2 Variance scale.
 *
 * @return the log probability density.
 */
function logpdf_matrix_normal_inverse_wishart_multivariate_gaussian<
    Arg1,Arg2,Arg3,Arg4,Arg5,Arg6,Arg7,Arg8>(x:Arg1, a:Arg2, N:Arg3, Λ:Arg4,
    Γ:Arg5, k:Arg6, c:Arg7, φ2:Arg8) -> {
  let L <- chol(Λ);
  let O <- trisolve(L, N);
  let M <- triinnersolve(L, O);
  let Ψ <- Γ - inner(O);
  let Σ <- (φ2 + dot(trisolve(L, a)))*Ψ;
  return logpdf_multivariate_t(x, k, inner(M, a) + c, Σ);
}

/*
 * Update the parameters of a Gaussian variate with linear transformation
 * of matrix-normal-inverse-Wishart prior.
 *
 * @param x The variate.
 * @param a Scale.
 * @param N Prior among-row concentration times mean.
 * @param Λ Prior among-row concentration.
 * @param Γ Prior accumulator for among-column spread.
 * @param k Prior degrees of freedom.
 * @param c Offset.
 * @param φ2 Additional variance scale.
 *
 * @return the posterior hyperparameters `N'`, `Λ'`, `Γ'` and `k'`.
 */
function update_matrix_normal_inverse_wishart_multivariate_gaussian<
    Arg1,Arg2,Arg3,Arg4,Arg5,Arg6,Arg7,Arg8>(x:Arg1, a:Arg2, N:Arg3, Λ:Arg4,
    Γ:Arg5, k:Arg6, c:Arg7, φ2:Arg8) -> {
  let Λ' <- Λ + outer(a/sqrt(φ2));
  let N' <- N + outer(a/φ2, x - c);
  let Γ' <- Γ + outer((x - c)/sqrt(φ2));
  let k' <- k + 1;
  return make_matrix_normal_inverse_wishart(N', Λ', Γ', k');
}
