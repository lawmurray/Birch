/**
 * Matrix normal-inverse-Wishart distribution.
 *
 * !!! note
 *     This class does not inherit from MatrixTDistribution, as it is
 *     typically used for a random variable that is marginalized out.
 */
final class MatrixNormalInverseWishartDistribution<Arg1,Arg2,Arg3,Arg4>(
    N:Arg1, Λ:Arg2, Γ:Arg3, k:Arg4) < Distribution<Real[_,_]> {
  /**
   * Among-row concentration times mean.
   */
  N:Arg1 <- N;

  /**
   * Among-row concentration.
   */
  Λ:Arg2 <- Λ;
  
  /**
   * Accumulator for among-column spread.
   */
  Γ:Arg3 <- Γ;
  
  /**
   * Among-column degrees of freedom.
   */
  k:Arg4 <- k;

  override function simulate() -> Real[_,_] {
    return value(simulate_matrix_normal_inverse_wishart(N, Λ, Γ, k));
  }

  override function simulateLazy() -> Real[_,_] {
    return eval(simulate_matrix_normal_inverse_wishart(N, Λ, Γ, k));
  }
  
  override function logpdf(X:Real[_,_]) -> Real! {   
    return value(logpdf_matrix_normal_inverse_wishart(X, N, Λ, Γ, k));
  }

  override function logpdfLazy(X:Real[_,_]) -> Real! {   
    return eval(logpdf_matrix_normal_inverse_wishart(X, N, Λ, Γ, k));
  }

  override function update(X:Real[_,_]) -> Delay? {
    return update_matrix_normal_inverse_wishart(X, N, Λ, Γ, k);
  }

  override function updateLazy(X:Random<Real[_,_]>) -> Delay? {
    return update_matrix_normal_inverse_wishart(X, N, Λ, Γ, k);
  }

  override function hoist() -> Expression<Real> {   
    return box(logpdf_matrix_normal_inverse_wishart(random(), N, Λ, Γ, k));
  }

  override function fix() {
    super.fix();
    constant(N);
    constant(Λ);
    constant(Γ);
    constant(k);

    if hasJoined() {
      let (Y, A, N, Λ, Γ, k, Φ) <-
          getJoined().joinMatrixNormalInverseWishartMultivariateGaussian()!;
      setUpdated(update_matrix_normal_inverse_wishart_matrix_gaussian(
          value(Y), value(A), value(N), value(Λ), value(Γ), value(k),
          value(Φ)));
      setJoined(nil);
    }
  }

  override function isMatrixNormalInverseWishart() -> Boolean {
    return !x?;
  }

  override function getMatrixNormalInverseWishart() -> (Expression<Real[_,_]>,
      Expression<Real[_,_]>, Expression<Real[_,_]>, Expression<Real>)? {
    assert isMatrixNormalInverseWishart();
    return box(N, Λ, Γ, k);
  }

  override function write(buffer:Buffer) {
    buffer.set("class", "MatrixNormalInverseWishart");
    buffer.set("N", value(N));
    buffer.set("Λ", value(Λ));
    buffer.set("Γ", value(Γ));
    buffer.set("k", value(k));
  }
}

function MatrixNormalInverseWishart<Arg1,Arg2,Arg3,Arg4>(M:Arg1, Σ:Arg2,
    Ψ:Arg3, k:Arg4) -> Distribution<Real[_,_]> {
  let L <- chol(Σ);
  let O <- trisolve(L, M);
  let N <- triinnersolve(L, O);
  let Λ <- cholinv(L);
  let Γ <- Ψ + inner(O);
  return make_matrix_normal_inverse_wishart(N, Λ, Γ, k);
}
function make_matrix_normal_inverse_wishart<Arg1,Arg2,Arg3,Arg4>(N:Arg1,
    Λ:Arg2, Γ:Arg3, k:Arg4) -> {
  return peg_matrix_normal_inverse_wishart(peg(N), peg(Λ),
      peg(Γ), peg(k));
}
function peg_matrix_normal_inverse_wishart<Arg1,Arg2,Arg3,Arg4>(N:Arg1,
    Λ:Arg2, Γ:Arg3, k:Arg4) -> {
  return MatrixNormalInverseWishartDistribution<Arg1,Arg2,Arg3,Arg4>(N, Λ, Γ,
      k);
}

/**
 * Create matrix Gaussian distribution.
 */
function MatrixGaussian<Arg1,Arg2>(M:Arg1, U:Arg2, V:Random<Real[_,_]>) ->
    Distribution<Real[_,_]> {
  if V.isInverseWishart() {
    let p_V <- V.graft();
    let (Ψ, k) <- p_V.getInverseWishart()!;
    let L <- chol(U);
    let O <- trisolve(L, M);
    let N <- triinnersolve(L, O);
    let Λ <- cholinv(L);
    let Γ <- Ψ + inner(O);
    let p <- make_matrix_normal_inverse_wishart(N, Λ, Γ, k);
    p_V.setMarginalized(p);
    return p;
  } else {
    return make_matrix_gaussian(M, U, V);
  }
}

/*
 * Simulate a matrix normal-inverse-Wishart distribution.
 *
 * @param N Among-row concentration times mean.
 * @param Λ Among-row concentration.
 * @param Γ Accumulator for among-column spread.
 * @param k Degrees of freedom.
 */
function simulate_matrix_normal_inverse_wishart<Arg1,Arg2,Arg3,Arg4>(N:Arg1,
    Λ:Arg2, Γ:Arg3, k:Arg4) -> {
  let L <- chol(Λ);
  let O <- trisolve(L, N);
  let M <- triinnersolve(L, O);
  let Σ <- cholinv(L);
  let Ψ <- Γ - inner(O);
  return simulate_matrix_t(k, M, Σ, Ψ);
}

/*
 * Observe a matrix normal-inverse-Wishart variate.
 *
 * @param X The variate.
 * @param N Among-row concentration times mean.
 * @param Λ Among-row concentration.
 * @param Γ Accumulator for among-column spread.
 * @param k Prior degrees of freedom.
 *
 * @return the log probability density.
 */
function logpdf_matrix_normal_inverse_wishart<Arg1,Arg2,Arg3,Arg4,Arg5>(
    X:Arg1, N:Arg2, Λ:Arg3, Γ:Arg4, k:Arg5) -> {
  let L <- chol(Λ);
  let O <- trisolve(L, N);
  let M <- triinnersolve(L, O);
  let Σ <- cholinv(L);
  let Ψ <- Γ - inner(O);
  return logpdf_matrix_t(X, k, M, Σ, Ψ);
}

/*
 * Update the parameters of a matrix normal-inverse-Wishart variate.
 *
 * @param X The variate.
 * @param N Among-row concentration times mean.
 * @param Λ Among-row concentration.
 * @param Γ Accumulator for among-column spread.
 * @param k Prior degrees of freedom.
 *
 * @return the posterior hyperparameters `Ψ'` and `k'`.
 */
function update_matrix_normal_inverse_wishart<Arg1,Arg2,Arg3,Arg4,Arg5>(
    X:Arg1, N:Arg2, Λ:Arg3, Γ:Arg4, k:Arg5) -> {
  let n <- rows(X);
  let L <- chol(Λ);
  let O <- trisolve(L, N);
  let M <- triinnersolve(L, O);
  let Ψ' <- Γ - inner(O) + inner(triinner(L, X - M));
  let k' <- k + n;
  return make_inverse_wishart(Ψ', k');
}
