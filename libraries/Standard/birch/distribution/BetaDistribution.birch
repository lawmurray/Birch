/**
 * Beta distribution.
 */
final class BetaDistribution<Arg1,Arg2>(α:Arg1, β:Arg2) < Distribution<Real> {
  /**
   * First shape.
   */
  α:Arg1 <- α;

  /**
   * Second shape.
   */
  β:Arg2 <- β;

  override function simulate() -> Real! {
    return value(simulate_beta(α, β));
  }

  override function simulateLazy() -> Real! {
    return eval(simulate_beta(α, β));
  }
  
  override function logpdf(x:Real!) -> Real! {
    return value(logpdf_beta(x, α, β));
  }

  override function logpdfLazy(x:Real!) -> Real! {
    return eval(logpdf_beta(x, α, β));
  }

  override function hoist() -> Expression<Real> {
    return box(logpdf_beta(random(), α, β));
  }

  override function cdf(x:Real!) -> Real!? {
    return cdf_beta(x, value(α), value(β));
  }

  override function lower() -> Real!? {
    return 0.0;
  }
  
  override function upper() -> Real!? {
    return 1.0;
  }

  override function fix() {
    super.fix();
    constant(α);
    constant(β);
  }

  override function isBeta() -> Boolean {
    return !x?;
  }

  override function getBeta() -> (Expression<Real>, Expression<Real>)? {
    assert isBeta();
    return box(α, β);
  }

  override function write(buffer:Buffer) {
    buffer.set("class", "Beta");
    buffer.set("α", value(α));
    buffer.set("β", value(β));
  }
}

/**
 * Create beta distribution.
 */
function Beta<Arg1,Arg2>(α:Arg1, β:Arg2) -> Distribution<Real> {
  return make_beta(α, β);
}
function make_beta<Arg1,Arg2>(α:Arg1, β:Arg2) -> {
  return peg_beta(peg(α), peg(β));
}
function peg_beta<Arg1,Arg2>(α:Arg1, β:Arg2) -> {
  return BetaDistribution<Arg1,Arg2>(α, β);
}

/*
 * Observe a beta variate.
 *
 * @param x The variate.
 * @param α Shape.
 * @param β Shape.
 *
 * @return the log probability density.
 */
function logpdf_beta<Arg1,Arg2,Arg3>(x:Arg1, α:Arg2, β:Arg3) -> {
  return (α - 1.0)*log(x) + (β - 1.0)*log1p(-x) - lbeta(α, β);
}

/*
 * CDF of a beta variate.
 *
 * @param x The variate.
 * @param α Shape.
 * @param β Shape.
 *
 * @return the cumulative probability.
 */
function cdf_beta<Arg1,Arg2,Arg3>(x:Arg1, α:Arg2, β:Arg3) -> {
  return ibeta(α, β, x);
}
