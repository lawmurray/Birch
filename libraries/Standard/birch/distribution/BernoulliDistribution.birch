/**
 * Bernoulli distribution.
 */
final class BernoulliDistribution<Arg>(ρ:Arg) < Distribution<Boolean> {
  /**
   * Success probability.
   */
  ρ:Arg <- ρ;

  override function simulate() -> Boolean! {
    return value(simulate_bernoulli(ρ));
  }
  
  override function simulateLazy() -> Boolean! {
    return eval(simulate_bernoulli(ρ));
  }
  
  override function logpdf(x:Boolean!) -> Real! {
    return value(logpdf_bernoulli(x, ρ));
  }

  override function logpdfLazy(x:Boolean!) -> Real! {
    return eval(logpdf_bernoulli(x, ρ));
  }

  override function hoist() -> Expression<Real> {
    return box(logpdf_bernoulli(random(), ρ));
  }

  override function fix() {
    super.fix();
    constant(ρ);
  }

  override function write(buffer:Buffer) {
    buffer.set("class", "Bernoulli");
    buffer.set("ρ", value(ρ));
  }
}

/**
 * Create Bernoulli distribution.
 */
function Bernoulli<Arg>(ρ:Arg) -> Distribution<Boolean> {
  return make_bernoulli(ρ);
}
function make_bernoulli<Arg>(ρ:Arg) -> {
  return construct<BernoulliDistribution<Arg>>(ρ);
}

/*
 * Observe a Bernoulli variate.
 *
 * @param x The variate.
 * @param ρ Probability of a true result.
 *
 * @return the log probability mass.
 */
function logpdf_bernoulli<Arg1,Arg2>(x:Arg1, ρ:Arg2) -> {
  return where(x, log(ρ), log1p(-ρ));
}
