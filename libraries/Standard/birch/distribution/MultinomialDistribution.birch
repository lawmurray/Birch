/**
 * Multinomial distribution.
 */
final class MultinomialDistribution<Arg1,Arg2>(n:Arg1, ρ:Arg2) <
    Distribution<Integer[_]> {
  /**
   * Number of trials.
   */
  n:Arg1 <- n;

  /**
   * Category probabilities.
   */
  ρ:Arg2 <- ρ;

  override function simulate() -> Integer[_] {
    return simulate_multinomial(value(n), value(ρ));
  }

  override function simulateLazy() -> Integer[_] {
    return simulate_multinomial(eval(n), eval(ρ));
  }

  override function logpdf(x:Integer[_]) -> Real! {
    return value(logpdf_multinomial(x, n, ρ));
  }

  override function logpdfLazy(x:Integer[_]) -> Real! {
    return eval(logpdf_multinomial(x, n, ρ));
  }

  override function hoist() -> Expression<Real> {
    return box(logpdf_multinomial(random(), n, ρ));
  }

  override function fix() {
    super.fix();
    constant(n);
    constant(ρ);
  }

  override function write(buffer:Buffer) {
    buffer.set("class", "Multinomial");
    buffer.set("n", value(n));
    buffer.set("ρ", value(ρ));
  }
}

/**
 * Create multinomial distribution.
 */
function Multinomial<Arg1,Arg2>(n:Arg1, ρ:Arg2) -> Distribution<Integer[_]> {
  return make_multinomial(n, ρ);
}
function make_multinomial<Arg1,Arg2>(n:Arg1, ρ:Arg2) -> {
  return peg_multinomial(peg(n), peg(ρ));
}
function peg_multinomial<Arg1,Arg2>(n:Arg1, ρ:Arg2) -> {
  return MultinomialDistribution<Arg1,Arg2>(n, ρ);
}

/*
 * Simulate a multinomial distribution.
 *
 * @param n Number of trials.
 * @param ρ Category probabilities. These should sum to one.
 *
 * This uses an $\mathcal{O}(N)$ implementation based on:
 *
 * Bentley, J. L. and J. B. Saxe (1979). Generating sorted lists of random
 * numbers. Technical Report 2450, Carnegie Mellon University, Computer
 * Science Department.
 */
function simulate_multinomial(n:Integer, ρ:Real[_]) -> Integer[_] {
  return simulate_multinomial(n, ρ, 1.0);
}

/*
 * Simulate a multinomial distribution.
 *
 * @param n Number of trials.
 * @param ρ Unnormalized category probabilities.
 * @param Z Sum of the unnormalized category probabilities.
 *
 * This uses an $\mathcal{O}(N)$ implementation based on:
 *
 * Bentley, J. L. and J. B. Saxe (1979). Generating sorted lists of random
 * numbers. Technical Report 2450, Carnegie Mellon University, Computer
 * Science Department.
 */
function simulate_multinomial(n:Integer, ρ:Real[_], Z:Real) -> Integer[_] {
  assert length(ρ) > 0;
  assert abs(sum(ρ)! - Z) < 1.0e-6;

  let D <- length(ρ);
  let R <- ρ[D];
  let lnMax <- 0.0;
  let j <- D;
  let i <- n;
  let x <- vector(0, D);

  while i > 0 {
    lnMax <- lnMax + log(simulate_uniform(0.0, 1.0))/i;
    u:Real <- Z*exp(lnMax);
    while u < Z - R {
      j <- j - 1;
      R <- R + ρ[j];
    }
    x[j] <- x[j] + 1;
    i <- i - 1;
  }
  while j > 1 {
    j <- j - 1;
    x[j] <- 0;
  }
  return x;
}

/*
 * Observe a multinomial variate.
 *
 * @param x The variate.
 * @param n Number of trials.
 * @param ρ Category probabilities.
 *
 * @return the log probability mass.
 */
function logpdf_multinomial<Arg1,Arg2,Arg3>(x:Arg1, n:Arg2, ρ:Arg3) -> {
  assert length(x) == length(ρ);
  return lfact(n) + sum(hadamard(x, log(ρ)) - lfact(x));
}
