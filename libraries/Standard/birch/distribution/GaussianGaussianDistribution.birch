/**
 * Gaussian distribution with linear transformation of Gaussian prior.
 */
final class GaussianGaussianDistribution<Arg1,Arg2,Arg3,Arg4,Arg5>(a:Arg1,
    μ:Arg2, σ2:Arg3, c:Arg4, ω2:Arg5) < Distribution<Real> {
  /**
   * Scale.
   */
  a:Arg1 <- a;
    
  /**
   * Prior mean.
   */
  μ:Arg2 <- μ;

  /**
   * Prior variance.
   */
  σ2:Arg3 <- σ2;

  /**
   * Offset.
   */
  c:Arg4 <- c;

  /**
   * Likelihood variance.
   */
  ω2:Arg5 <- ω2;

  override function simulate() -> Real! {
    return value(simulate_gaussian(a*μ + c, a*a*σ2 + ω2));
  }

  override function simulateLazy() -> Real! {
    return eval(simulate_gaussian(a*μ + c, a*a*σ2 + ω2));
  }
  
  override function logpdf(x:Real!) -> Real! {
    return value(logpdf_gaussian(x, a*μ + c, a*a*σ2 + ω2));
  }

  override function logpdfLazy(x:Real!) -> Real! {
    return eval(logpdf_gaussian(x, a*μ + c, a*a*σ2 + ω2));
  }

  override function update(x:Real!) -> Delay? {
    return update_gaussian_gaussian(x, a, μ, σ2, c, ω2);
  }

  override function updateLazy(x:Random<Real>) -> Delay? {
    return update_gaussian_gaussian(x, a, μ, σ2, c, ω2);
  }

  override function hoist() -> Expression<Real> {
    return box(logpdf_gaussian(random(), a*μ + c, a*a*σ2 + ω2));
  }

  override function cdf(x:Real!) -> Real!? {
    return cdf_gaussian(x, value(a*μ + c), value(a*a*σ2 + ω2));
  }

  override function fix() {
    super.fix();
    constant(a);
    constant(μ);
    constant(σ2);
    constant(c);
    constant(ω2);
  }

  override function isGaussian() -> Boolean {
    return !x?;
  }

  override function getGaussian() -> (Expression<Real>, Expression<Real>)? {
    assert isGaussian();
    return box(a*μ + c, a*a*σ2 + ω2);
  }
}

function GaussianGaussian<Arg1,Arg2,Arg3,Arg4,Arg5>(a:Arg1, μ:Arg2, σ2:Arg3,
    c:Arg4, ω2:Arg5) -> Distribution<Real> {
  return make_gaussian_gaussian(a, μ, σ2, c, ω2);
}
function make_gaussian_gaussian<Arg1,Arg2,Arg3,Arg4,Arg5>(a:Arg1, μ:Arg2,
    σ2:Arg3, c:Arg4, ω2:Arg5) -> {
  return peg_gaussian_gaussian(peg(a), peg(μ), peg(σ2), peg(c), peg(ω2));
}
function peg_gaussian_gaussian<Arg1,Arg2,Arg3,Arg4,Arg5>(a:Arg1, μ:Arg2,
    σ2:Arg3, c:Arg4, ω2:Arg5) -> {
  return GaussianGaussianDistribution<Arg1,Arg2,Arg3,Arg4,Arg5>(a,
      μ, σ2, c, ω2);
}

/**
 * Create Gaussian distribution.
 */
function Gaussian<Arg1,Arg2,Arg3>(μ:Add<Mul<Arg1,Random<Real>>,Arg2>,
    σ2:Arg3) -> Distribution<Real> {
  let a <- μ.l().l();
  let m <- μ.l().r();
  let c <- μ.r();
  if m.isGaussian() {
    let p_m <- m.graft();
    let (m1, ω2) <- p_m.getGaussian()!;
    let p <- make_gaussian_gaussian(a, m1, ω2, c, σ2);
    p_m.setMarginalized(p);
    return p;
  } else {
    return make_gaussian(μ, σ2);
  }
}

/**
 * Create Gaussian distribution.
 */
function Gaussian<Arg1,Arg2>(μ:Mul<Arg1,Random<Real>>, σ2:Arg2) ->
    Distribution<Real> {
  return Gaussian(μ + 0.0, σ2);
}

/**
 * Create Gaussian distribution.
 */
function Gaussian<Arg1,Arg2>(μ:Add<Random<Real>,Arg1>, σ2:Arg2) ->
    Distribution<Real> {
  return Gaussian(1.0*μ.l() + μ.r(), σ2);
}

/**
 * Create Gaussian distribution.
 */
function Gaussian<Arg>(μ:Random<Real>, σ2:Arg) -> Distribution<Real> {
  return Gaussian(1.0*μ + 0.0, σ2);
}

/*
 * Update the parameters of a Gaussian distribution with linear transformation
 * of Gaussian prior on the mean.
 *
 * @param x The variate.
 * @param a Scale.
 * @param μ Prior mean.
 * @param σ2 Prior variance.
 * @param c Offset.
 * @param ω2 Likelihood variance.
 *
 * @return the posterior hyperparameters `μ'` and `σ2'`.
 */
function update_gaussian_gaussian<Arg1,Arg2,Arg3,Arg4,Arg5,Arg6>(
    x:Arg1, a:Arg2, μ:Arg3, σ2:Arg4, c:Arg5, ω2:Arg6) -> {
  let σ2' <- 1.0/(1.0/σ2 + a*a/ω2);
  let μ' <- σ2'*(μ/σ2 + a*(x - c)/ω2);
  return make_gaussian(μ', σ2');
}
