/*
 * Distribution over the first of a pair of random variables given their sum.
 * This is produced by AddDiscreteDeltaDistribution.update() once the sum is
 * realized. In turn, AddDiscreteFirstDistribution.update() produces a Delta
 * distribution over the second of the pair once the first is realized.
 */
final class AddDiscreteFirstDistribution<Arg1>(p:Arg1, a:Integer!,
    b:Integer!) < Distribution<Integer> {
  /**
   * Probability vector of the pairs.
   */
  p:Arg1 <- p;

  /**
   * Inclusive lower bound on the first random variable.
   */
  a:Integer! <- a;

  /**
   * Inclusive lower bound on the second random variable.
   */
  b:Integer! <- b;
  
  override function simulate() -> Integer! {
    return value(simulate_add_discrete_first(p, a, b));
  }

  override function simulateLazy() -> Integer! {
    return eval(simulate_add_discrete_first(p, a, b));
  }

  override function logpdf(x:Integer!) -> Real! {
    return value(logpdf_add_discrete_first(x, p, a, b));
  }

  override function logpdfLazy(x:Integer!) -> Real! {
    return eval(logpdf_add_discrete_first(x, p, a, b));
  }

  override function update(x:Integer!) -> Delay? {
    return update_add_discrete_first(x, p, a, b);
  }

  override function updateLazy(x:Random<Integer>) -> Delay? {
    return update_add_discrete_first(x, p, a, b);
  }

  override function hoist() -> Expression<Real> {
    return box(logpdf_add_discrete_first(random(), p, a, b));
  }

  override function lower() -> Integer!? {
    return a;
  }
  
  override function upper() -> Integer!? {
    return a + length(p) - 1;
  }

  override function fix() {
    super.fix();
    constant(p);
    constant(a);
    constant(b);
  }

  override function isBoundedDiscrete() -> Boolean {
    return !x?;
  }

  override function getBoundedDiscrete() -> (Expression<Real[_]>, Integer!)? {
    assert isBoundedDiscrete();
    return (box(p), a);
  }
}

function AddDiscreteConstrained<Arg1>(p:Arg1, a:Integer!, b:Integer!) ->
    Distribution<Integer> {
  return make_add_discrete_first(p, a, b);
}
function make_add_discrete_first<Arg1>(p:Arg1, a:Integer!, b:Integer!) -> {
  return peg_add_discrete_first(peg(p), a, b);
}
function peg_add_discrete_first<Arg1>(p:Arg1, a:Integer!, b:Integer!) -> {
  return AddDiscreteFirstDistribution<Arg1>(p, a, b);
}

/*
 * Simulate the first of a pair of random variables of a given sum.
 *
 * @param p Probability vector of the pairs.
 * @param a Inclusive lower bound of first random variable.
 * @param b Inclusive lower bound of second random variable.
 */
function simulate_add_discrete_first<Arg1>(p:Arg1, a:Integer!,
    b:Integer!) -> {
  return simulate_categorical(p) + a - 1;
}

/*
 * Observe the first of a pair of random variables of a given sum.
 *
 * @param x The variate.
 * @param p Probability vector of the pairs.
 * @param a Inclusive lower bound of first random variable.
 * @param b Inclusive lower bound of second random variable.
 *
 * @return the probability mass.
 */
function logpdf_add_discrete_first<Arg1,Arg2>(x:Arg1, p:Arg2, a:Integer!,
    b:Integer!) -> {
  return log(element(p, x - a + 1));
}

/*
 * Update the first of a pair of random variables of a given sum.
 *
 * @param x The variate.
 * @param p Probability vector of the pairs.
 * @param a Inclusive lower bound of first random variable.
 * @param b Inclusive lower bound of second random variable.
 *
 * @return the updated distribution.
 */
function update_add_discrete_first<Arg1,Arg2>(x:Arg1, p:Arg2, a:Integer!,
    b:Integer!) -> {
  return make_delta(a + b + length(p) - 1 - x);
}
