/**
 * Wishart distribution.
 */
final class WishartDistribution<Arg1,Arg2>(Ψ:Arg1, k:Arg2) < Distribution<Real[_,_]> {
  /**
   * Scale.
   */
  Ψ:Arg1 <- Ψ;
  
  /**
   * Degrees of freedom.
   */
  k:Arg2 <- k;

  override function simulate() -> Real[_,_] {
    return value(simulate_scaled_wishart(Ψ, k));
  }

  override function simulateLazy() -> Real[_,_] {
    return eval(simulate_scaled_wishart(Ψ, k));
  }
  
  override function logpdf(X:Real[_,_]) -> Real! {
    return value(logpdf_wishart(X, Ψ, k));
  }

  override function logpdfLazy(X:Real[_,_]) -> Real! {
    return eval(logpdf_wishart(X, Ψ, k));
  }

  override function hoist() -> Expression<Real> {
    return box(logpdf_wishart(random(), Ψ, k));
  }

  override function fix() {
    super.fix();
    constant(Ψ);
    constant(k);
  }

  override function write(buffer:Buffer) {
    buffer.set("class", "Wishart");
    buffer.set("Ψ", value(Ψ));
    buffer.set("k", value(k));
  }
}

/**
 * Create Wishart distribution.
 */
function Wishart<Arg1,Arg2>(Ψ:Arg1, k:Arg2) -> Distribution<Real[_,_]> {
  return make_wishart(Ψ, k);
}
function make_wishart<Arg1,Arg2>(Ψ:Arg1, k:Arg2) -> {
  return peg_wishart(peg(Ψ), peg(k));
}
function peg_wishart<Arg1,Arg2>(Ψ:Arg1, k:Arg2) -> {
  return WishartDistribution<Arg1,Arg2>(Ψ, k);
}

hpp{{
//using numbirch::simulate_wishart;
}}

/*
 * Simulate a Wishart distribution.
 *
 * @param Ψ Scale matrix.
 * @param k Degrees of freedom.
 */
function simulate_scaled_wishart<Arg1,Arg2>(Ψ:Arg1, k:Arg2) -> {
  assert rows(Ψ) == columns(Ψ);
  let n <- rows(Ψ);
  return triouter(trimul(chol(Ψ), simulate_wishart(k, n)));
}

/*
 * Observe a Wishart variate.
 *
 * @param X The variate.
 * @param Ψ Scale matrix.
 * @param k Degrees of freedom.
 *
 * @return the log probability density.
 */
function logpdf_wishart<Arg1,Arg2,Arg3>(X:Arg1, Ψ:Arg2, k:Arg3) -> {
  let p <- columns(X);
  let l <- k + (p + 1.0);
  let L <- chol(X);
  let J <- chol(Ψ);
  return 0.5*(frobenius(trisolve(J, L)) - l*p*log(2.0)) + l*ltridet(J) +
      k*ltridet(L) - lgamma(0.5*l, p);
}
