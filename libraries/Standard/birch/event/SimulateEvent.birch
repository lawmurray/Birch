/**
 * Event triggered by a *simulate*, typically from the `<~` operator.
 *
 * - p: Associated distribution.
 *
 * ```mermaid
 * classDiagram
 *    Event <|-- SimulateEvent
 *    Event <|-- ObserveEvent
 *    Event <|-- AssumeEvent
 *    Event <|-- FactorEvent
 *    link Event "../Event/"
 *    link SimulateEvent "../SimulateEvent/"
 *    link ObserveEvent "../ObserveEvent/"
 *    link AssumeEvent "../AssumeEvent/"
 *    link FactorEvent "../FactorEvent/"
 * ```
 */
final class SimulateEvent<Value>(p:Distribution<Value>) < Event {
  /**
   * Value.
   */
  x:Value?;
  
  /**
   * Distribution.
   */
  p:Distribution<Value> <- p;

  function value() -> Value {
    return x!;
  }

  function record() -> Record {
    return SimulateRecord(x!);
  }
  
  function coerce(record:Record) -> SimulateRecord<Value> {
    auto r <- SimulateRecord<Value>?(record);
    if !r? {
      error("incompatible trace");
    }
    return r!;
  }

  function accept(handler:PlayHandler) {
    handler.doHandle(this);
  }
  
  function accept(handler:MoveHandler) {
    handler.doHandle(this);
  }

  function accept(record:Record, handler:PlayHandler) {
    handler.doHandle(coerce(record), this);
  }

  function accept(record:Record, handler:MoveHandler) {
    handler.doHandle(coerce(record), this);
  }
}

/**
 * Create a SimulateEvent.
 */
function SimulateEvent<Value>(p:Distribution<Value>) -> SimulateEvent<Value> {
  return construct<SimulateEvent<Value>>(p);
}
