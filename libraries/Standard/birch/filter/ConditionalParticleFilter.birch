/**
 * Conditional particle filter.
 *
 * ```mermaid
 * classDiagram
 *    ParticleFilter <|-- AliveParticleFilter
 *    ParticleFilter <|-- MoveParticleFilter
 *    ParticleFilter <|-- ConditionalParticleFilter
 *    link ParticleFilter "../ParticleFilter/"
 *    link AliveParticleFilter "../AliveParticleFilter/"
 *    link MoveParticleFilter "../MoveParticleFilter/"
 *    link ConditionalParticleFilter "../ConditionalParticleFilter/"
 * ```
 */
class ConditionalParticleFilter < ParticleFilter {
  /**
   * Tape<Record> of the reference particle. This will have no value for the first
   * iteration of the filter. Subsequent iterations will draw a particle from
   * the previous iteration to condition the new iteration, setting this
   * variable.
   */
  r:Tape<Record>?;

  /**
   * Chosen particle index. This is the index, at the final step, of the
   * particle chosen as a weighted sample from the target distribution,
   * used as the reference particle for the next iteration of the conditional
   * particle filter.
   */
  b:Integer <- 0;

  /**
   * Should ancestor sampling be used?
   */
  ancestor:Boolean <- false;

  /**
   * Will the enclosing sampler initialize the initial state of the reference
   * particle? This is set by ParticleGibbs, which initializes `x` externally
   * after the first iteration.
   */
  alreadyInitialized:Boolean <- false;

  function particle(archetype:Model) -> Particle {
    return ConditionalParticle(archetype);
  }

  override function initialize(archetype:Model) {
    super.initialize(archetype);
    b <- 1;
  }

  override function filter(t:Integer) {
    if r? && ancestor {
      ancestorSample(t);
    }
    resample(t);
    step(t);
    reduce();
  }

  function ancestorSample(t:Integer) {
    auto w' <- w;
    dynamic parallel for n in 1..nparticles {
      auto x' <- clone(x[n]);
      auto r' <- clone(r!);
      auto handler <- PlayHandler(delayed);
      handler.input <- r';
      with (handler) {
        x'.m.simulate(t);
        w'[n] <- w'[n] + handler.w;
      }
      ///@todo Don't assume Markov model here
    }
    b <- global.ancestor(w');
  }

  override function start() {
    if !alreadyInitialized {
      parallel for n in 1..nparticles {
        auto x <- ConditionalParticle?(this.x[n])!;
        auto handler <- PlayHandler(delayed);
        if r? && n == b {
          handler.input <- r!;
        }
        handler.output <- x.trace;
        with (handler) {
          x.m.simulate();
        }
        w[n] <- handler.w;
      }
    }
  }

  override function step(t:Integer) {
    parallel for n in 1..nparticles {
      auto x <- ConditionalParticle?(this.x[n])!;
      auto handler <- PlayHandler(delayed);
      if r? && n == b {
        handler.input <- r!;
      }
      handler.output <- x.trace;
      with (handler) {
        x.m.simulate(t);
      }
      w[n] <- w[n] + handler.w;
    }
  }

  override function resample(t:Integer) {
    if ess <= trigger*nparticles {
      if r? {
        (a, b) <- conditional_resample_multinomial(w, b);
      } else {
        a <- resample_multinomial(w);
      }
      w <- vector(0.0, nparticles);
      dynamic parallel for n in 1..nparticles {
        if a[n] != n {
          x[n] <- clone(x[a[n]]);
        }
      }
      collect();
    } else {
      /* normalize weights to sum to nparticles */
      w <- w - vector(lsum - log(Real(nparticles)), nparticles);
    }
  }

  override function read(buffer:Buffer) {
    super.read(buffer);
    ancestor <-? buffer.get("ancestor", ancestor);
  }

  override function write(buffer:Buffer) {
    super.write(buffer);
    buffer.set("ancestor", ancestor);
  }
}
