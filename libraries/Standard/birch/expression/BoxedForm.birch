/**
 * Boxed form. Memoizes forward evaluation, acting as a checkpoint for
 * reverse-mode automatic differentiation.
 *
 * @param Value Result type.
 * @param Form Form form.
 */
final class BoxedForm<Value,Form>(f:Form) < DoExpression<Value> {
  /**
   * Expression form.
   */
  f:Form? <- f;

  override function doEval() {
    this.x <- global.eval(f!);
  }

  override function doMove(visitor:MoveVisitor) {
    this.x <- global.move(f!, visitor);
  }

  override function doArgs(visitor:ArgsVisitor) {
    global.args(f!, visitor);
  }

  override function doShallowGrad(visitor:GradVisitor) {
    global.shallow_grad(f!, this.g!, visitor);
  }

  override function doDeepGrad(visitor:GradVisitor) {
    global.deep_grad(f!, visitor);
  }

  override function doReset() {
    global.reset(f!);
  }

  override function doRelink(visitor:RelinkVisitor) {
    global.relink(f!, visitor);
  }

  override function doConstant() {
    global.constant(f!);
    f <- nil;
  }
}

hpp{{
namespace birch {

template<class Value, class Form>
struct is_expression<BoxedForm<Value,Form>> {
  static constexpr bool value = true;
};

}
}}
