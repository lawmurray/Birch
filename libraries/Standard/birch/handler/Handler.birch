/**
 * Event handler that eagerly computes weights.
 *
 * ```mermaid
 * classDiagram
 *    Handler <|-- PlayHandler
 *    Handler <|-- MoveHandler
 *    link Handler "../Handler/"
 *    link PlayHandler "../PlayHandler/"
 *    link MoveHandler "../MoveHandler/"
 * ```
 */
abstract class Handler {
  /**
   * Input trace, if any.
   */
  input:Tape<Record>?;

  /**
   * Output trace, if any.
   */
  output:Tape<Record>?;

  /**
   * Accumulated weight.
   */
  w:Real <- 0.0;

  /**
   * Handle an event.
   *
   * - event: The event.
   *
   * This calls one of the `doHandle()` member functions according to the
   * current state.
   */
  final function handle(event:Event) {
    if input? {
      doHandle(input!.here(), event);
      input!.next();
    } else {
      doHandle(event);
    }
    if output? {
      output!.pushBack(event.record());
    }
  }

  /**
   * Handle an event.
   *
   * - event: The event.
   */
  abstract function doHandle(event:Event);

  /**
   * Handle an event with an input.
   *
   * - record: The input record.
   * - event: The event.
   */
  abstract function doHandle(record:Record, event:Event);
}
