/**
 * Event triggered for a factor.
 *
 * - w: Associated log-weight.
 *
 * ```mermaid
 * classDiagram
 *    Event <|-- SimulateEvent
 *    Event <|-- ObserveEvent
 *    Event <|-- AssumeEvent
 *    Event <|-- FactorEvent
 *    link Event "../Event/"
 *    link SimulateEvent "../SimulateEvent/"
 *    link ObserveEvent "../ObserveEvent/"
 *    link AssumeEvent "../AssumeEvent/"
 *    link FactorEvent "../FactorEvent/"
 * ```
 */
final class FactorEvent(w:Expression<Real>) < Event {
  /**
   * Associated log-weight.
   */
  w:Expression<Real> <- w;
  
  function record() -> Record {
    return FactorRecord();
  }

  function coerce(record:Record) -> FactorRecord {
    let r <- FactorRecord?(record);
    if !r? {
      error("incompatible trace");
    }
    return r!;
  }

  function accept(handler:PlayHandler) {
    handler.doHandle(this);
  }
  
  function accept(handler:MoveHandler) {
    handler.doHandle(this);
  }

  function accept(record:Record, handler:PlayHandler) {
    handler.doHandle(coerce(record), this);
  }

  function accept(record:Record, handler:MoveHandler) {
    handler.doHandle(coerce(record), this);
  }
}

/**
 * Create a FactorEvent.
 */
function FactorEvent(w:Expression<Real>) -> FactorEvent {
  return construct<FactorEvent>(w);
}

/**
 * Create a FactorEvent.
 */
function FactorEvent(w:Real) -> FactorEvent {
  return FactorEvent(box(w));
}
