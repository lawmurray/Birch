/**
 * Object value.
 */
class ObjectValue < Value {
  /**
   * Entries of the object. This uses Array rather than List or similar to
   * avoid reaching stack size limits for large data sets, and to keep
   * entries in the order given.
   */
  entries:Array<Entry>;

  function accept(writer:Writer) {
    writer.visit(this);
  }

  function isObject() -> Boolean {
    return true;
  }

  function getChild(name:String) -> Buffer? {
    let iter <- entries.walk();
    while iter.hasNext() {
      let entry <- iter.next();
      if entry.name == name {
        return entry.buffer;
      }
    }
    return nil;
  }
  
  function setChild(name:String) -> Buffer {
    buffer:Buffer;
    entry:Entry;
    entry.name <- name;
    entry.buffer <- buffer;
    entries.pushBack(entry);
    return buffer;    
  }
}

function ObjectValue() -> ObjectValue {
  return construct<ObjectValue>();
}
