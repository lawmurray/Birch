/**
 * Wishart distribution.
 */
final class WishartDistribution<Arg1,Arg2>(Ψ:Arg1, k:Arg2) < Distribution<Real[_,_]> {
  /**
   * Scale.
   */
  Ψ:Arg1 <- Ψ;
  
  /**
   * Degrees of freedom.
   */
  k:Arg2 <- k;

  override function simulate() -> Real[_,_] {
    return simulate_wishart(global.value(Ψ), global.value(k));
  }

  override function simulateLazy() -> Real[_,_] {
    return simulate_wishart(global.eval(Ψ), global.eval(k));
  }
  
  override function logpdf(X:Real[_,_]) -> Real! {
    return logpdf_wishart(X, global.value(Ψ), global.value(k));
  }

  override function logpdfLazy(X:Real[_,_]) -> Real! {
    return logpdf_wishart(X, global.eval(Ψ), global.eval(k));
  }

  override function hoist() -> Expression<Real> {
    return box(logpdf_wishart(this, Ψ, k));
  }

  override function doConstant() {
    super.constant();
    global.constant(Ψ);
    global.constant(k);
  }

  override function write(buffer:Buffer) {
    buffer.set("class", "Wishart");
    buffer.set("Ψ", global.value(Ψ));
    buffer.set("k", global.value(k));
  }
}

/**
 * Create Wishart distribution.
 */
function Wishart<Arg1,Arg2>(Ψ:Arg1, k:Arg2) -> Distribution<Real[_,_]> {
  return wrap_wishart(Ψ, k);
}
function wrap_wishart<Arg1,Arg2>(Ψ:Arg1, k:Arg2) -> {
  return make_wishart(wrap(Ψ), wrap(k));
}
function make_wishart<Arg1,Arg2>(Ψ:Arg1, k:Arg2) -> {
  return construct<WishartDistribution<Arg1,Arg2>>(Ψ, k);
}

hpp{{
using numbirch::standard_wishart;
}}

/*
 * Simulate a Wishart distribution.
 *
 * @param Ψ Scale matrix.
 * @param k Degrees of freedom.
 */
function simulate_wishart<Arg1,Arg2>(Ψ:Arg1, k:Arg2) -> {
  assert rows(Ψ) == columns(Ψ);
  let n <- rows(Ψ);
  let A <- standard_wishart(k, n);
  return triouter(trimul(chol(Ψ), A));
}

/*
 * Observe a Wishart variate.
 *
 * @param X The variate.
 * @param Ψ Scale matrix.
 * @param k Degrees of freedom.
 *
 * @return the log probability density.
 */
function logpdf_wishart<Arg1,Arg2,Arg3>(X:Arg1, Ψ:Arg2, k:Arg3) -> {
  let p <- columns(X);
  let l <- wrap(k + (p + 1.0));
  let L <- wrap(chol(X));
  let J <- wrap(chol(Ψ));
  return 0.5*(frobenius(trisolve(J, L)) - l*p*log(2.0)) + l*ltridet(J) +
      k*ltridet(L) - lgamma(0.5*l, p);
}
