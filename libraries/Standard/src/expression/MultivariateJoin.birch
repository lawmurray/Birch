/**
 * Lazy `join`.
 */
acyclic class MultivariateJoin<Value>(x:Expression<Value>[_]) <
    MultivariateExpression<Value[_]> {
  /**
   * Arguments.
   */
  y:Expression<Value>[_]? <- x;

  override function doDepth() -> Integer {
    let depth <- 0;
    for i in 1..length() {
      depth <- max(depth, y![i].depth());
    }
    return depth + 1;
  }

  override function doRows() -> Integer {
    return global.length(y!);
  }

  override function doValue() -> Value[_] {
    return transform(y!, \(x:Expression<Value>) -> Value {
        return x.value();
      });
  }
  
  override function doPilot(gen:Integer) -> Value[_] {
    return transform(y!, \(x:Expression<Value>) -> Value {
        return x.pilot(gen);
      });
  }

  override function doGet() -> Value[_] {
    return transform(y!, \(x:Expression<Value>) -> Value {
        return x.get();
      });
  }

  override function doMove(gen:Integer, κ:Kernel) -> Value[_] {
    return transform(y!, \(x:Expression<Value>) -> Value {
        return x.move(gen, κ);
      });
  }
  
  override function doGrad(gen:Integer) {
    for_each(y!, d!, \(x:Expression<Value>, d:Value) { x.grad(gen, d); });
  }

  override function doPrior() -> Expression<Real>? {
    p:Expression<Real>?;
    let L <- length();
    for i in 1..L {
      let q <- y![i].prior();
      if q? {
        if p? {
          p <- p! + q!;
        } else {
          p <- q;
        }
      }
    }
    return p;
  }

  override function doCompare(gen:Integer, x:DelayExpression,
      κ:Kernel) -> Real {
    assert rows() == x.rows();
    assert columns() == x.columns();
    
    let o <- MultivariateJoin<Value>?(x)!;
    let w <- 0.0;
    let R <- rows();
    let C <- columns();
    for i in 1..R {
      for j in 1..C {
        w <- w + y![i,j].compare(gen, o.y![i,j], κ);
      }
    }
    return w;
  }

  override function doCount(gen:Integer) {
    for_each(y!, \(x:Expression<Value>) { x.count(gen); });
  }

  override function doConstant() {
    for_each(y!, \(x:Expression<Value>) { x.constant(); });
  }

  override function doDetach() {
    y <- nil;
  }
}

/**
 * Lazy `join`. Converts a vector of scalar expressions into a vector
 * expression.
 */
function join<Value>(y:Expression<Value>[_]) -> MultivariateJoin<Value> {
  return construct<MultivariateJoin<Value>>(y);
}

/**
 * Lazy `split`. Converts a vector expression into a vector of scalar
 * expressions.
 */
function split<Value>(y:Expression<Value[_]>) -> Expression<Value>[_] {
  let z <- canonical(y);
  // ^ canonical(y) above is an identity function for all but Random objects;
  //   for these it wraps the Random in an additional expression that can
  //   accumulate gradients by element (which a Random cannot) before passing
  //   the whole vector of accumulated gradients onto the Random

  return vector(\(i:Integer) -> Expression<Value> {
        return construct<MultivariateElement<Value>>(z, i);
      }, z.length());
}
