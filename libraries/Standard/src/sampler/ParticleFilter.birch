/**
 * Particle filter.
 *
 * ```mermaid
 * classDiagram
 *    ParticleFilter <|-- AliveParticleFilter
 *    link ParticleFilter "../ParticleFilter/"
 *    link AliveParticleFilter "../AliveParticleFilter/"
 * ```
 */
class ParticleFilter {
  /**
   * Particles.
   */
  x:Array<Model>;

  /**
   * Log weights.
   */
  w:Real[_];

  /**
   * Time of last resample.
   */
  r:Integer <- 0;

  /**
   * Logarithm of sum of weights.
   */
  lsum:Real <- 0.0;

  /**
   * Effective sample size.
   */
  ess:Real <- 0.0;

  /**
   * Log normalizing constant.
   */
  lnormalize:Real <- 0.0;

  /**
   * Number of propagations. This is not the same as the number of particles;
   * the number of propagations performed may, according to the filter type,
   * differ from the number of particles, such as for alive and rejection
   * control particle filters.
   */
  npropagations:Integer <- 0;

  /**
   * Accept rate of moves.
   */
  raccepts:Real?;

  /**
   * Number of particles.
   */
  nparticles:Integer <- 1;

  /**
   * Threshold for resampling. Resampling is performed whenever the
   * effective sample size, as a proportion of `N`, drops below this
   * threshold.
   */
  trigger:Real <- 0.7;

  /**
   * Should automatic marginalization and conditioning be enabled?
   */
  autoconj:Boolean <- true;

  /**
   * Should automatic differentiation be enabled?
   */
  autodiff:Boolean <- false;

  /**
   * Should automatic joining be enabled?
   */
  autojoin:Boolean <- false;

  /**
   * Start filter.
   *
   * @param model Model.
   * @param input Input buffer.
   */
  function filter(model:Model, input:Buffer) {
    x.clear();
    global.bridge(model);
    for n in 1..nparticles {
      x.pushBack(global.copy(model));
    }
    w <- vector(0.0, nparticles);
    r <- 0;
    ess <- nparticles;
    lsum <- 0.0;
    lnormalize <- 0.0;
    npropagations <- nparticles;
    simulate(input);
  }

  /**
   * Step filter.
   *
   * @param t Step number, beginning at 1.
   * @param input Input buffer.
   * @param κ Markov kernel.
   */
  function filter(t:Integer, input:Buffer, κ:Kernel?) {
    resample(t, κ);
    simulate(t, input);
  }

  /**
   * Step filter.
   *
   * @param t Step number, beginning at 1.
   * @param input Input buffer.
   */
  function filter(t:Integer, input:Buffer) {
    filter(t, input, nil);
  }

  /**
   * Start particles.
   *
   * @param input Input buffer.
   */
  function simulate(input:Buffer) {
    parallel for n in 1..nparticles {
      let h <- construct<Handler>(autoconj, autodiff, autojoin);
      with h {
        x[n].read(input);
        x[n].simulate();
      }
      x[n].Ξ.pushBack(h.Ξ);
      x[n].Φ.pushBack(h.Φ);
      cpp{{
      w.slice(n) = w.slice(n) + h->w;
      }}
    }
    (ess, lsum) <- resample_reduce(w);
    lnormalize <- lnormalize + lsum - log(nparticles);
    npropagations <- nparticles;
  }

  /**
   * Step particles.
   *
   * @param t Step number.
   * @param input Input buffer.
   */
  function simulate(t:Integer, input:Buffer) {
    parallel for n in 1..nparticles {
      let h <- construct<Handler>(autoconj, autodiff, autojoin);
      with h {
        x[n].read(t, input);
        x[n].simulate(t);
      }
      x[n].Ξ.pushBack(h.Ξ);
      x[n].Φ.pushBack(h.Φ);
      cpp{{
      w.slice(n) = w.slice(n) + h->w;
      }}
    }
    (ess, lsum) <- resample_reduce(w);
    lnormalize <- lnormalize + lsum - log(nparticles);
    npropagations <- nparticles;
  }

  /**
   * Resample particles.
   *
   * @param t Step number.
   * @param κ Markov kernel.
   */
  function resample(t:Integer, κ:Kernel?) {
    if r < t {
      r <- t;
      raccepts <- nil;
      if ess <= trigger*nparticles {
        /* resample */
        let (a, o) <- resample_systematic(w);

        /* bridge-find */
        dynamic parallel for n in 1..nparticles {
          if o[n] >= 2 {
            /* will be copied at least once, perform bridge-finding */
            bridge(x[n]);
          }
        }

        /* copy */
        dynamic parallel for n in 1..nparticles {
          if a[n] != n {
            /* a[n] != n implies o[n] >= 2; see permute_ancestors() */
            x[n] <- copy(x[a[n]]);
          }
        }

        /* many particles won't have survived, good time to cycle collect */
        collect();

        /* move */
        if κ? {
          let α <- vector(0.0, nparticles);  // acceptance rate per particle
          parallel for n in 1..nparticles {
            α[n] <- κ!.move(x[n]);
          }
          raccepts <- sum(α)/nparticles;

          /* update acceptance rate errors for next scale update */
          κ!.adapt(raccepts!);
        }

        /* reset weights */
        w <- vector(0.0, nparticles);
      } else {
        /* normalize weights to sum to nparticles */
        w <- w - (lsum - log(nparticles));
        collect();
      }
    }
  }

  /**
   * Reconfigure particle filter.
   *
   * @param autoconj Should automatic marginalization and conditioning be enabled?
   * @param autodiff Should automatic differentiation be enabled?
   * @param autojoin Should automatic joining be enabled?
   */
  function reconfigure(autoconj:Boolean, autodiff:Boolean, autojoin:Boolean) {
    this.autoconj <- autoconj;
    this.autodiff <- autodiff;
    this.autojoin <- autojoin;
  }

  override function read(buffer:Buffer) {
    nparticles <-? buffer.get<Integer>("nparticles");
    trigger <-? buffer.get<Real>("trigger");
    autoconj <-? buffer.get<Boolean>("autoconj");
    autodiff <-? buffer.get<Boolean>("autodiff");
    autojoin <-? buffer.get<Boolean>("autojoin");
  }
}
