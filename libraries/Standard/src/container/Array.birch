/**
 * Resizeable array with constant-time random access.
 *
 * @tparam Type Element type. Must be default-constructible.
 */
final class Array<Type> {
  phantom values;
  hpp{{
  std::vector<Type> values;
  }}

  /**
   * Reference to an element.
   */
  operator [i:Integer] -> Type {
    while size() < i {
      pushBack();
    }
    cpp{{
    return values[i - 1];
    }}
  }

  /**
   * Number of elements.
   */
  function size() -> Integer {
    cpp{{
    return values.size();
    }}
  }

  /**
   * Is this empty?
   */
  function empty() -> Boolean {
    return size() == 0;
  }

  /**
   * Clear all elements.
   */
  function clear() {
    cpp{{
    values.clear();
    }}
  }
  
  /**
   * Get an element.
   *
   * @param i Position.
   */
  function get(i:Integer) -> Type {
    while size() < i {
      pushBack();
    }
    cpp{{
    return values[i - 1];
    }}
  }

  /**
   * Set an element.
   *
   * @param i Position.
   * @param x Value.
   */
  function set(i:Integer, x:Type) {
    while size() < i - i {
      pushBack();
    }
    if size() == i - 1 {
      pushBack(x);
    } else {
      cpp{{
      values[i - 1] = x;
      }}
    }
  }

  /**
   * Get the first element.
   */
  function front() -> Type {
    assert size() > 0;
    return get(1);
  }

  /**
   * Get the last element.
   */
  function back() -> Type {
    assert size() > 0;
    return get(size());
  }

  /**
   * Insert an element at the front.
   *
   * @param x Value.
   */
  function pushFront(x:Type) {
    insert(1, x);
  }

  /**
   * Insert a new default-constructed element at the front and return it.
   */
  function pushFront() -> Type {
    let x <- make<Type>();
    if x? {
      pushFront(x!);
    } else {
      error("not default constructible");
    }
    return x!;
  }

  /**
   * Insert an element at the back.
   *
   * @param x Value.
   */
  function pushBack(x:Type) {
    insert(size() + 1, x);
  }

  /**
   * Insert a new default-constructed element at the back and return it.
   */
  function pushBack() -> Type {
    let x <- make<Type>();
    if x? {
      pushBack(x!);
    } else {
      error("not default constructible");
    }
    return x!;
  }

  /**
   * Remove the first element.
   */
  function popFront() {
    erase(1);
  }

  /**
   * Remove the last element.
   */
  function popBack() {
    erase(size());
  }
  
  /**
   * Insert a new element.
   *
   * @param i Position.
   * @param x Value.
   *
   * Inserts the new element immediately before the current element at
   * position `i`. To insert at the back of the container, use a position that
   * is one more than the current size, or `pushBack()`.
   */
  function insert(i:Integer, x:Type) {
    assert 1 <= i && i <= size() + 1;
    cpp{{
    values.insert(values.begin() + i - 1, x);
    }}
  }

  /**
   * Erase an element.
   *
   * @param i Position.
   *
   * The size decreases by one.
   */
  function erase(i:Integer) {
    assert 1 <= i && i <= size();
    cpp{{
    values.erase(values.begin() + i - 1);
    }}
  }

  /**
   * Erase multiple elements.
   *
   * @param i Position.
   * @param n Number of elements.
   *
   * The size decreases by `n`.
   */
  function erase(i:Integer, n:Integer) {
    assert 1 <= i && i <= size();
    assert 1 <= n && n <= size() - i + 1;
    cpp{{
    values.erase(values.begin() + i - 1, values.begin() + i - 1 + n);
    }}
  }

  /**
   * Obtain an iterator.
   *
   * @return an iterator across elements in forward order.
   */
  function walk() -> Iterator<Type> {
    return construct<ArrayIterator<Type>>(this);
  }

  override function read(buffer:Buffer) {
    clear();
    let iter <- buffer.walk();
    while iter.hasNext() {
      let x <- iter.next().get<Type>();
      assert x?;
      pushBack(x!);
    }
  }

  override function write(buffer:Buffer) {
    buffer.setEmptyArray();
    let f <- walk();
    while f.hasNext() {
      buffer.push(f.next());
    }
  }

  override function read(t:Integer, buffer:Buffer) {
    clear();
    let iter <- buffer.walk();
    while iter.hasNext() {
      let x <- iter.next().get<Type>(t);
      assert x?;
      pushBack(x!);
    }
  }

  override function write(t:Integer, buffer:Buffer) {
    buffer.setEmptyArray();
    let f <- walk();
    while f.hasNext() {
      buffer.push(t, f.next());
    }
  }
}
