/**
 * Input stream. Supports sequential reading only, ignoring white space.
 */
class InputStream {
  /**
   * File handle.
   */
  file:File?;

  /**
   * Open file.
   *
   *   @param path Path.
   *   @param mode Mode. 
   */
  function open(path:String, mode:Integer) {
    assert !(file?);
    file <- fopen(path, mode);
  }

  /**
   * Open file with default mode.
   *
   *   @param path Path.
   */
  function open(path:String) {
    open(path, READ);
  }

  /**
   * Close file.
   */
  function close() {
    assert file?;
    cpp{{
    fclose(file.value());
    }}
    file <- nil;
  }

  /**
   * Check for end-of-file.
   */
  function eof() -> Boolean {
    assert file?;
    cpp{{
    return feof(file.value());
    }}
  }

  /**
   * Read integer.
   */
  function scanInteger() -> Integer? {
    assert file?;
    x:Integer?;
    cpp{{
    long long int y;  // ensure that fscanf gets the exact type it expects
    auto res = ::fscanf(this->file.value(), "%lld", &y);
    if (res == 1) {
      x = Integer(y);
    } 
    }}
    return x;
  }

  /**
   * Read real.
   */
  function scanReal() -> Real? {
    assert file?;
    x:Real?;
    cpp{{
    double y;  // ensure that fscanf gets the exact type it expects
    if (::fscanf(this->file.value(), "%lf", &y) == 1) {
      x = Real(y);
    } 
    }}
    return x;
  }
}

/**
 * Create an input stream from an already-open file.
 */
function make_input_stream(file:File) -> InputStream {
  o:InputStream;
  o.file <- file;
  return o;
}
