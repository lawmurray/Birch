/**
 * Write data to a file.
 *
 * ```mermaid
 * classDiagram
 *    Writer <|-- YAMLWriter
 *    YAMLWriter <|-- JSONWriter
 *    link Writer "../Writer/"
 *    link YAMLWriter "../YAMLWriter/"
 *    link JSONWriter "../JSONWriter/"
 * ```
 *
 * Typical use is to use the `Writer` factory function to instantiate an
 * object of an appropriate derived class based on the file extension of the
 * given path:
 *
 *     let writer <- make_writer(path);
 *
 * The whole contents of the file can be written at once with:
 *
 *     writer.dump(buffer);
 *
 * Alternatively, where the root element of the file is an array, the contents
 * may be written sequentially, one element at a time:
 *
 *     writer.push(buffer1);
 *     writer.push(buffer2);
 *     writer.push(buffer3);
 *
 * Finally, close the file:
 *
 *     writer.close();
 *
 * A file may not be valid for reading until the writer is closed, depending
 * on the file format.
 */
abstract class Writer {
  /**
   * Open a file.
   *
   * @param path Path of the file.
   */
  abstract function open(path:String);
  
  /**
   * Write the whole contents of a buffer into the file.
   *
   * @param buffer Buffer to write.
   */
  abstract function dump(buffer:Buffer);

  /**
   * Write a buffer to the file. This allows for sequential writing of the
   * file, where the root element is an array. It avoids holding the entire
   * file in memory at once.
   *
   * @param buffer Buffer to write.
   */
  abstract function push(buffer:Buffer);

  /**
   * Flush accumulated writes to the file.
   */
  abstract function flush();
  
  /**
   * Close the file.
   */
  abstract function close();

  abstract function visit(keys:Array<String>, values:Array<Buffer>);
  abstract function visit(values:Array<Buffer>);
  abstract function visit(value:String);
  abstract function visit(value:Real);
  abstract function visit(value:Integer);
  abstract function visit(value:Boolean);
  abstract function visit(value:Real[_]);
  abstract function visit(value:Integer[_]);
  abstract function visit(value:Boolean[_]);
  abstract function visit(value:Real[_,_]);
  abstract function visit(value:Integer[_,_]);
  abstract function visit(value:Boolean[_,_]);
  abstract function visitNil();

  final function visit(value:Real!) {
    return visit(value!);
  }

  final function visit(value:Integer!) {
    return visit(value!);
  }

  final function visit(value:Boolean!) {
    return visit(value!);
  }
}

/**
 * Create a writer for a file.
 *
 * @param path Path of the file.
 *
 * @return the writer.
 *
 * The file extension of `path` is used to determine the precise type of the
 * returned object. Supported file extension are `.json` and `.yml`.
 */
function make_writer(path:String) -> Writer {
  let ext <- extension(path);
  result:Writer?;
  if ext == ".json" {
    writer:JSONWriter;
    writer.open(path);
    result <- writer;
  } else if ext == ".yml" {
    writer:YAMLWriter;
    writer.open(path);
    result <- writer;
  }
  if !result? {
    error("unrecognized file extension '" + ext + "' in path '" + path +
        "'; supported extensions are '.json' and '.yml'.");
  }
  return result!;
}

/**
 * Write the whole contents of a buffer into a file.
 *
 * @param path Path of the file.
 * @param buffer The contents.
 */
function dump(path:String, buffer:Buffer) {
  let writer <- make_writer(path);
  writer.dump(buffer);
  writer.close();
}
