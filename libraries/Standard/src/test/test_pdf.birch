/*
 * Test the pmf of a univariate Boolean distribution.
 *
 * - π: The target distribution. 
 * - N: Number of samples.
 * - lazy: Use lazy version?
 */
function test_pdf(π:Distribution<Boolean>, N:Integer, lazy:Boolean) {
  /* simulate, counting the occurrence of each value */
  let k <- 0;
  for n in 1..N {
    if π.simulate() {
      k <- k + 1;
    }
  }

  /* compare pdf to count */
  let failed <- false;
  let ε <- 5.0/sqrt(Real(N));
  
  p:Real;
  q:Real;
  if lazy && π.supportsLazy() {
    p <- exp(π.logpdfLazy(box(true))!.value());
    q <- exp(π.logpdfLazy(box(false))!.value());
  } else {
    p <- π.pdf(true);
    q <- π.pdf(false);
  }
  let δ <- abs(p - Real(k)/N);
  if δ > ε {
    failed <- true;
    stderr.print("failed on true, " + δ + " > " + ε + "\n");
  }
  δ <- abs(q - Real(N - k)/N);
  if δ > ε {
    failed <- true;
    stderr.print("failed on false, " + δ + " > " + ε + "\n");
  }
  if failed {
    exit(1);
  }
}

/*
 * Test the pmf of a univariate discrete distribution.
 *
 * - π: The target distribution. 
 * - N: Number of samples.
 * - lazy: Use lazy version?
 */
function test_pdf(π:Distribution<Integer>, N:Integer, lazy:Boolean) {  
  /* lower bound on interval */
  let from <- π.lower();
  if !from? {
    from <- π.quantile(1.0e-6);
    assert from?;
  }

  /* upper bound on interval */
  let to <- π.upper();
  if !to? {
    to <- π.quantile(1.0 - 1.0e-6);
    if !to? {
      /* search for a rough upper bound for the interval */
      let u <- 100;
      while π.pdf(u) > 1.0e-6 {
        u <- 2*u;
      }
      to <- u;
    }
  }

  /* simulate, counting the occurrence of each value */
  let count <- vector(0, to! - from! + 1);
  for n in 1..N {
    let i <- π.simulate() - from! + 1;
    if 1 <= i && i <= length(count) {
      count[i] <- count[i] + 1;
    }
  }

  /* compare sum of pdf to counts */
  let failed <- false;
  for x in from!..to! {
    let δ <- abs(π.pdf(x) - Real(count[x - from! + 1])/N);
    let ε <- 5.0/sqrt(Real(N));
    if δ > ε {
      failed <- true;
      stderr.print("failed on value " + x + ", " + δ + " > " + ε + "\n");
    }
  }
  if failed {
    exit(1);
  }
}

/*
 * Test a multivariate pdf.
 *
 * - π: The target distribution. 
 * - D: Number of dimensions.
 * - N: Number of samples.
 * - B: Number of samples in each batch for tuning Metropolis sampler.
 * - S: Metropolis sampler skip.
 * - lazy: Use lazy version?
 */
function test_pdf(π:Distribution<Real[_]>, D:Integer, N:Integer, B:Integer,
    S:Integer, lazy:Boolean) {  
  X1:Real[N,D];
  X2:Real[N,D];
  
  /* iid samples */
  for n in 1..N {
    X1[n,1..D] <- canonical(π.simulate());
  }
  
  /* compute the shape for a Gaussian proposal using the iid samples */
  let μ <- vector(0.0, D);
  let Σ <- matrix(0.0, D, D);
  for n in 1..N {
    let x <- X1[n,1..D];
    μ <- μ + x;
    Σ <- Σ + outer(x);
  }
  μ <- μ/Real(N);
  Σ <- Σ/Real(N) - outer(μ);
  
  /* scale this proposal to get a reasonable acceptance rate */
  let done <- false;
  do {
    let a <- 0.0;
    let x <- π.simulate();
    l:Real;
    if lazy && π.supportsLazy() {
      l <- π.logpdfLazy(box(x))!.value();
    } else {
      l <- π.logpdf(x);
    }

    for n in 1..B {
      let x' <- simulate_multivariate_gaussian(x, llt(Σ));
      l':Real;
      if lazy && π.supportsLazy() {
        l' <- π.logpdfLazy(box(x'))!.value();
      } else {
        l' <- π.logpdf(x');
      }
      if log(simulate_uniform(0.0, 1.0)) <= l' - l {
        /* accept */
        x <- x';
        l <- l';
        a <- a + 1.0/B;
      }
    }
    //stderr.print("trial acceptance rate: " + a + "\n");
    if a < 0.2 {
      Σ <- 0.5*Σ;
    } else if a > 0.4 {
      Σ <- 1.5*Σ;
    } else {
      done <- true;
    }
  } while !done;

  /* apply the Metropolis sampler */
  let a <- 0.0;
  let x <- π.simulate();
  l:Real;
  if lazy && π.supportsLazy() {
    l <- π.logpdfLazy(box(x))!.value();
  } else {
    l <- π.logpdf(x);
  }
  for n in 1..N {
    for s in 1..S {
      let x' <- simulate_multivariate_gaussian(x, llt(Σ));
      l':Real;
      if lazy && π.supportsLazy() {
        l' <- π.logpdfLazy(box(x'))!.value();
      } else {
        l' <- π.logpdf(x');
      }
      if log(simulate_uniform(0.0, 1.0)) <= l' - l {
        /* accept */
        x <- x';
        l <- l';
        a <- a + 1.0/(N*S);
      }
    }
    X2[n,1..D] <- x;
  }
  //stderr.print("final acceptance rate: " + a + "\n");
  
  /* test distance between the iid and Metropolis samples */
  if !pass(X1, X2) {
    exit(1);
  }
}

/*
 * Test a matrix pdf.
 *
 * - π: The target distribution. 
 * - R: Number of rows.
 * - C: Number of columns.
 * - N: Number of samples.
 * - B: Number of samples in each batch for tuning Metropolis sampler.
 * - S: Metropolis sampler skip.
 * - lazy: Use lazy version?
 */
function test_pdf(π:Distribution<Real[_,_]>, R:Integer, C:Integer, N:Integer,
    B:Integer, S:Integer, lazy:Boolean) {
  X1:Real[N,R*C];
  X2:Real[N,R*C];
  
  /* iid samples */
  for n in 1..N {
    X1[n,1..R*C] <- vec(π.simulate());
  }
  
  /* compute the shape for a Gaussian proposal using the iid samples */
  let μ <- vector(0.0, R*C);
  let Σ <- matrix(0.0, R*C, R*C);
  for n in 1..N {
    let x <- X1[n,1..R*C];
    μ <- μ + x;
    Σ <- Σ + outer(x);
  }
  μ <- μ/Real(N);
  Σ <- Σ/Real(N) - outer(μ);
  
  /* scale this proposal to get a reasonable acceptance rate */
  let done <- false;
  do {
    let a <- 0.0;
    let x <- π.simulate();
    l:Real;
    if lazy && π.supportsLazy() {
      l <- π.logpdfLazy(box(x))!.value();
    } else {
      l <- π.logpdf(x);
    }
    for n in 1..B {
      let x' <- mat(simulate_multivariate_gaussian(vec(x), llt(Σ)), C);
      l':Real;
      if lazy && π.supportsLazy() {
        l' <- π.logpdfLazy(box(x'))!.value();
      } else {
        l' <- π.logpdf(x');
      }
      if log(simulate_uniform(0.0, 1.0)) <= l' - l {
        /* accept */
        x <- x';
        l <- l';
        a <- a + 1.0/B;
      }
    }
    //stderr.print("trial acceptance rate: " + a + "\n");
    if a < 0.2 {
      Σ <- 0.5*Σ;
    } else if a > 0.4 {
      Σ <- 1.5*Σ;
    } else {
      done <- true;
    }
  } while !done;

  /* apply the Metropolis sampler */
  let a <- 0.0;
  let x <- π.simulate();
  l:Real;
  if lazy && π.supportsLazy() {
    l <- π.logpdfLazy(box(x))!.value();
  } else {
    l <- π.logpdf(x);
  }
  for n in 1..N {
    for s in 1..S {
      let x' <- mat(simulate_multivariate_gaussian(vec(x), llt(Σ)), C);
      l':Real;
      if lazy && π.supportsLazy() {
        l' <- π.logpdfLazy(box(x'))!.value();
      } else {
        l' <- π.logpdf(x');
      }
      if log(simulate_uniform(0.0, 1.0)) <= l' - l {
        /* accept */
        x <- x';
        l <- l';
        a <- a + 1.0/(N*S);
      }
    }
    X2[n,1..R*C] <- vec(x);
  }
  //stderr.print("final acceptance rate: " + a + "\n");
  
  /* test distance between the iid and Metropolis samples */
  if !pass(X1, X2) {
    exit(1);
  }
}
