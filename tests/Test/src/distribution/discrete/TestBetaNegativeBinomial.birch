class TestBetaNegativeBinomial < TestModel {
  k:Random<Integer>;
  α:Random<Real>;
  β:Random<Real>;
  ρ:Random<Real>;
  x:Random<Integer>;

  override function initialize() {
    k ~ UniformInteger(1, 100);
    α ~ Uniform(1.0, 20.0);
    β ~ Uniform(1.0, 20.0);
  }

  override function simulate() {
    ρ ~ Beta(α, β);
    x ~ NegativeBinomial(k, ρ);
  }

  override function forward() -> Real[_] {
    y:Real[2];
    y[1] <- ρ.eval();
    y[2] <- x.eval();
    return y;
  }

  override function backward() -> Real[_] {
    y:Real[2];
    assert !x.hasValue();
    y[2] <- x.eval();
    assert !ρ.hasValue();
    y[1] <- ρ.eval();
    return y;
  }

  function marginal() -> Distribution<Integer> {
    return x.getDistribution();
  }

  override function size() -> Integer {
    return 2;
  }
}

program test_conjugacy_beta_negative_binomial(N:Integer <- 10000, lazy:Boolean <- false) {
  m:TestBetaNegativeBinomial;
  test_conjugacy(m, N, lazy);
}

program test_pdf_beta_negative_binomial(N:Integer <- 10000,
    S:Integer <- 0, lazy:Boolean <- false) {
  m:TestBetaNegativeBinomial;
  test_pdf(m, N, lazy);
}

program test_grad_beta_negative_binomial(N:Integer <- 1000,
    backward:Boolean <- false) {
  m:TestBetaNegativeBinomial;
  test_grad(m, N, backward);
}
