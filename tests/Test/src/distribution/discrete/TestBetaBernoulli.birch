class TestBetaBernoulli < TestModel {
  α:Random<Real>;
  β:Random<Real>;
  ρ:Random<Real>;
  x:Random<Boolean>;

  override function initialize() {
    //
  }

  override function simulate() {
    α ~ Uniform(1.0, 20.0);
    β ~ Uniform(1.0, 20.0);
    ρ ~ Beta(α, β);
    x ~ Bernoulli(ρ);
  }

  override function forward() -> Real[_] {
    y:Real[2];
    y[1] <- eval(ρ);
    y[2] <- eval(x);
    return y;
  }

  override function backward() -> Real[_] {
    y:Real[2];
    assert !x.hasValue();
    y[2] <- eval(x);
    assert !ρ.hasValue();
    y[1] <- eval(ρ);
    return y;
  }

  function marginal() -> Distribution<Boolean> {
    return x.getDistribution();
  }

  override function size() -> Integer {
    return 2;
  }
}

program test_conjugacy_beta_bernoulli(N:Integer <- 10000, lazy:Boolean <- false) {
  m:TestBetaBernoulli;
  test_conjugacy(m, N, lazy);
}

program test_pdf_beta_bernoulli(N:Integer <- 10000,
    S:Integer <- 0, lazy:Boolean <- false) {
  m:TestBetaBernoulli;
  test_pdf(m, N, lazy);
}

program test_grad_beta_bernoulli(N:Integer <- 1000,
    backward:Boolean <- false) {
  m:TestBetaBernoulli;
  test_grad(m, N, backward);
}
