class TestNegateDiscreteDelta < TestModel {
  ρ:Random<Real>;
  x:Random<Integer>;
  y:Random<Integer>;

  α:Real;
  β:Real;
  n:Integer;

  override function initialize() {
    α <- simulate_uniform(0.0, 10.0);
    β <- simulate_uniform(0.0, 10.0);
    n <- simulate_uniform_int(1, 100);
  }

  override function simulate() {
    ρ ~ Beta(α, β);
    x ~ Binomial(n, ρ);
    y ~ Delta(-x);
  }

  override function forward() -> Real[_] {
    z:Real[size()];
    z[1] <- eval(ρ);
    z[2] <- eval(x);
    z[3] <- eval(y);
    return z;
  }

  override function backward() -> Real[_] {
    z:Real[size()];
    assert !y.hasValue();
    z[3] <- eval(y);
    assert !x.hasValue();
    z[2] <- eval(x);
    assert !ρ.hasValue();
    z[1] <- eval(ρ);
    return z;
  }

  function marginal() -> Distribution<Integer> {
    return y.getDistribution();
  }

  override function size() -> Integer {
    return 3;
  }
}

program test_conjugacy_negate_discrete_delta(N:Integer <- 10000,
    lazy:Boolean <- false) {
  m:TestNegateDiscreteDelta;
  test_conjugacy(m, N, lazy);
}

program test_pdf_negate_discrete_delta(N:Integer <- 10000,
    S:Integer <- 0, lazy:Boolean <- false) {
  m:TestNegateDiscreteDelta;
  test_pdf(m, N, lazy);
}

program test_negate_discrete_delta(N:Integer <- 1000,
    backward:Boolean <- false) {
  m:TestNegateDiscreteDelta;
  test_grad(m, N, backward);
}
