class TestInverseGammaGamma < TestModel {
  θ:Random<Real>;
  x:Random<Real>;
  k:Random<Real>;
  α:Random<Real>;
  β:Random<Real>;

  override function initialize() {
    k ~ Uniform(1.0, 10.0);
    α ~ Uniform(3.0, 10.0);
    β ~ Uniform(0.1, 10.0);
  }

  override function simulate() {
    θ ~ InverseGamma(α, β);
    x ~ Gamma(k, θ);
  }

  override function forward() -> Real[_] {
    y:Real[2];
    y[1] <- θ.eval();
    y[2] <- x.eval();
    return y;
  }

  override function backward() -> Real[_] {
    y:Real[2];
    assert !x.hasValue();
    y[2] <- x.eval();
    assert !θ.hasValue();
    y[1] <- θ.eval();
    return y;
  }

  function marginal() -> Distribution<Real> {
    return x.getDistribution();
  }

  override function size() -> Integer {
    return 2;
  }
}

program test_conjugacy_inverse_gamma_gamma(N:Integer <- 10000, lazy:Boolean <- false) {
  m:TestInverseGammaGamma;
  test_conjugacy(m, N, lazy);
}

program test_pdf_inverse_gamma_gamma(N:Integer <- 10000,
    S:Integer <- 20, lazy:Boolean <- false) {
  m:TestInverseGammaGamma;
  test_pdf(m, N, S, lazy);
}

program test_z_inverse_gamma_gamma(N:Integer <- 10000, S:Integer <- 20,
    lazy:Boolean <- false) {
  m:TestInverseGammaGamma;
  test_z(m, N, lazy);
}

program test_cdf_inverse_gamma_gamma(N:Integer <- 10000) {
  m:TestInverseGammaGamma;
  test_cdf(m, N);
}

program test_grad_inverse_gamma_gamma(N:Integer <- 1000, backward:Boolean <- false) {
  m:TestInverseGammaGamma;
  test_grad(m, N, backward);
}
