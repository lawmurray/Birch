/*
 * Test a particular conjugacy.
 *
 * @param m Model.
 * @param N Number of samples.
 * @param lazy Use lazy version?
 */
function test_conjugacy(m:TestModel, N:Integer, lazy:Boolean) {  
  let D <- m.size();  
  X1:Real[N,D];
  X2:Real[N,D];

  parallel for n in 1..N {
    let forward <- construct<Handler>(false, lazy, true);
    let backward <- construct<Handler>(true, lazy, true);

    /* initialize consistently between forward and backward versions */
    let m' <- copy(m);
    with forward {
      m'.initialize();
    }

    /* simulate forward */
    let f <- copy(m');
    with forward {
      f.simulate();
      X1[n,1..D] <- f.forward();
    }

    /* simulate backward */
    let b <- copy(m');
    with backward {
      b.simulate();
      X2[n,1..D] <- b.backward();
    }
  }
  
  /* compare */
  if !pass(X1, X2) {
    exit(1);
  }
}
